!---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|---8;
Option,  warn,info;

Title, "Matching chromaticity for beam1 and beam2 of the LHC 6.4 lattice with MADX";


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!                        TARGET CHROMATICITY ;
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
QPRIME = 2.0;

!---------------------------------------------------------------;
!           CALL IN SELECTION, SEQUENCE and OPTICS FILES;
!---------------------------------------------------------------;

option, -echo,-warn;
call, file = "../share/LHC/LHCRing/V6.503/V6.5.seq";
call, file = "../share/LHC/LHCRing/V6.503/V6.5.coll.str";
option, echo, warn, -info;

Beam, particle = proton, sequence=lhcb1, energy = 450.0,
          NPART=1.05E11, sige=      4.5e-4 ;
Beam, particle = proton, sequence=lhcb2, energy = 450.0,
          bv = -1, NPART=1.05E11, sige=      4.5e-4;         !<<<<
! the 'bv' parameter indicates that beam2 rotates counter-clockwise

! Select beam1:
USE, period=lhcb1;

! Generate a TWISS table:
select, flag=twiss, clear;
select, flag=twiss, pattern="IP\.*",column=name,s,betx,bety;
TWISS,sequence=lhcb1,file=twiss.b1.data;

! Select beam2:
USE, period=lhcb2;

! Generate a TWISS table:
select, flag=twiss, clear;
select, flag=twiss, pattern="IP\.*",column=name,s,betx,bety;
TWISS,sequence=lhcb2,file=twiss.b2.data;

! Match the chromaticity:
match,sequence=lhcb1,lhcb2;
    vary, name=KSF.B1, step=0.00001;
    vary, name=KSD.B1, step=0.00001;
    vary, name=KSF.B2, step=0.00001;
    vary, name=KSD.B2, step=0.00001;
    global,sequence=lhcb1,DQ1=QPRIME;
    global,sequence=lhcb1,DQ2=QPRIME;
    global,sequence=lhcb2,DQ1=QPRIME;
    global,sequence=lhcb2,DQ2=QPRIME;
    Lmdif, calls=10, tolerance=1.0e-21;
endmatch;

! Generate a TWISS table:
select, flag=twiss, clear;
select, flag=twiss, pattern="IP\.*",column=name,s,betx,bety;
TWISS,sequence=lhcb1,file=twiss.b1.data;

! Generate a TWISS table:
select, flag=twiss, clear;
select, flag=twiss, pattern="IP\.*",column=name,s,betx,bety;
TWISS,sequence=lhcb2,file=twiss.b2.data;

stop;

