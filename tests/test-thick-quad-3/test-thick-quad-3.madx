
title,"Five cell test";

beam;

option,-echo;
call,file="../share/fivecell/fivecell.seq"; 

option,echo,warn,info; 

use,period=fivecell;   ! use needed for twiss (not necessarily needed before makethin)
select,flag=twiss,clear; 
select,flag=twiss,column=name,s,betx,alfx,dx,dpx,bety,alfy,dy,dpy,mux,muy,apertype;
twiss,sequence=fivecell,file="fivecell_thick.tfs";

!--- quads
! select, flag=makethin, class=quadrupole, slice= 100; ! slice all quadrupoles
! select, flag=makethin, class=quadrupole, slice= 0; ! turn quad slicing off
  select, flag=makethin, class=quadrupole, thick=true;

!--- bends
! select, flag=makethin, class="rbend", slice=0;    ! bend slicing off --- use this to test only quadrupole slicing   or at least use makedipedge
! select, flag=makethin, class="rbend", slice=2;    ! negative drift with dipedge      refer=centre    problem,   ok with flatten
  select, flag=makethin, class="rbend", slice=1;

makethin,sequence=fivecell,style=teapot,makedipedge=true;

use,period=fivecell;
twiss,sequence=fivecell,file="fivecell_thin.tfs";

save,sequence=fivecell,file="fivecell_thin.seq";
use,sequence=fivecell;

!--- check that the sliced sequence is quitable for SIXTRACK translation  http://mad.web.cern.ch/mad/madx.old/c6t/c6t.html

sixtrack;

!--- test MAD-X tracking   http://mad.web.cern.ch/mad/madx.old/thintrack/thintrack.html
track, onepass, dump; ! onepass=true no closed orbit computed before tracking
  start, x= 0.001, px= 0.002, y= 0, py= 0, t= 0.001, pt= 0.003;
  run,   turns= 1;
endtrack;

!--- cleanup after sixtrack
removefile, file="fc.2";
removefile, file="fc.3";
removefile, file="fc.3.aper";
removefile, file="fc.3.aux";
removefile, file="fc.8";
removefile, file="fc.34";

stop;
