! H. Burkhardt.  Updated for improved makethin select in summer 2005, further updated in 2011 for extended teapot and functionality
!
! cd /tmp/$LOGNAME/ ; rm -f /tmp/$LOGNAME/* ; cp -pf ~/mad/makethin/fivecell.* . ; madx < ~/mad/makethin/fivecell.madx >& fivecell.out ; ps2pdf madxplot.ps ; open madxplot.pdf
!
! diff /tmp/hbu/fivecell_thin.seq ~/mad/madX-examples/makethin/fivecell_thin.seq
!

title,"Five cell test";

beam;

option,-echo;
call,file="fivecell.seq"; ! on http://madx.web.cern.ch/madx/madX/examples/makethin/

setplot,xsize=28.0,ysize=19.0,rscale=1.5; !hbu defaults are: setplot,font=1,lwidth=5.,xsize=27.,ysize=19.,ascale=1.5;lscale=2.,sscale=2.,rscale=1.8,post=1,interpolate=false;  use rscale=1.5 to get smaller title

option,echo,warn,info; // use this to turn info on
use,period=fivecell;   ! use needed for twiss (not necessarily needed before makethin)
select,flag=twiss,clear; select,flag=twiss,column=name,s,betx,alfx,dx,dpx,bety,alfy,dy,dpy,mux,muy,apertype;
twiss,sequence=fivecell,file="fivecell_thick.tfs";

!-- Plot thick sequence
  title,"fivecell before makethin";
  plot,file=madxplot,vaxis1=betx,bety,vaxis2=dx,dy,haxis=s, vmin=0,-4,vmax=350,4, colour=100, range=#s/#e;

!--- quads
  select, flag=makethin, class=quadrupole, slice= PARAM1; ! slice all quadrupoles
!select, flag=makethin, class=quadrupole, thick=true;

! select, flag=makethin, class=quadrupole, slice= 0; ! turn quad slicing off

!--- bends
! select, flag=makethin, class="rbend", slice=0;    ! bend slicing off --- use this to test only quadrupole slicing   or at least use makedipedge
! select, flag=makethin, class="rbend", slice=2;    ! negative drift with dipedge      refer=centre    problem,   ok with flatten
  select, flag=makethin, class="rbend", slice=1;

makethin,sequence=fivecell,style=teapot,makedipedge=true;

use,period=fivecell;
twiss,sequence=fivecell,file="fivecell_thin.tfs";

!-- Plot thin sequence
  title,"fivecell after makethin";
  plot,file=madxplot,vaxis1=betx,bety,vaxis2=dx,dy,haxis=s, vmin=0,-4,vmax=350,4, colour=100, range=#s/#e;

save,sequence=fivecell,file="fivecell_thin.seq";
use,sequence=fivecell;

!--- check that the sliced sequence is quitable for SIXTRACK translation  http://mad.web.cern.ch/mad/madx.old/c6t/c6t.html

sixtrack, aperture;

!--- test MAD-X tracking   http://mad.web.cern.ch/mad/madx.old/thintrack/thintrack.html
track, onepass, dump; ! onepass=true no closed orbit computed before tracking
!  start, x= 0.001, px= 0, y= 0, py= 0, t= 0, pt= 0;
  start, x= 0.001, px= 0.002, y= 0, py= 0, t= 0.001, pt= 0.003;
  observe, place= #e;
  run,   turns= 1;
endtrack;

stop;
