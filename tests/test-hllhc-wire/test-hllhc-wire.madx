! S. Fartoukh March 2009
! Ref sample job for tracking with multipole field imperfection in nominal 
! LHC magnets using V6.503 in collision
! M. Giovannozzi November 2010

!---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|---8;
Option,  warn,info;
System,"mkdir -p temp";
System,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/runIII lhc";
System,"ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.3 slhc";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/errors/0705 wise";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503/WISE/After_sector_3-4_repair fidel";
system,"ln -fns /afs/cern.ch/user/n/nkarast/public/MADXTests/macro macro";
option,-echo,-info;

!---------------------------------------------------------------;
!           CALL IN SELECTION, MACRO's, SEQUENCE, BEAM, NRJ and OPTICS FILES;
!---------------------------------------------------------------;
call,file="slhc/toolkit/macro.madx";        !Call some macro facilities

 mylhcbeam=1 ;! LHC beam 1 (clockwise)

! Set this flag if the file is not used as a mask file (sets %SEEDRAN to 1)
not_a_mask=0;
! Set this flag to correct the errors of D2 in the NLC (warning: for now only correcting b3 of D2, still in development)
correct_for_D2=0;
! Set this flag to correct the errors of MCBXF in the NLC (warning: this might be less reproducable in reality, use with care)
correct_for_MCBX=0;

!!********************************************************************
!!Manual Thin Sequence building
!!********************************************************************
Option, -echo,-warn,-info;
if (mylhcbeam==4){
  call,file="lhc/lhcb4.seq"; bvaux=1;
} else {
  call,file="lhc/lhc.seq"; bvaux=-1;
};
Option, -echo,warn,-info;
!
!!Install HL-LHC
call,file="slhc/hllhc_sequence.madx";

!Install MBH
!mbh_rm_mcs=0; mbh_rm_mcdo=0; !=1 to remove spools
!mbh_ir2q8=0;mbh_ir2q10=1;
!mbh_ir7q8=1;mbh_ir7q10=1;
!mbh_ir1q8=0;mbh_ir1q9=1;mbh_ir1q10=0;
!mbh_ir5q8=0;mbh_ir5q9=1;mbh_ir5q10=0;
!call,file="slhc/toolkit/install_mbh.madx";

! Slice nominal sequence
exec,myslice;

call,file="slhc/errors/install_mqxf_fringenl.madx";    ! adding fringe place holder
call,file="slhc/errors/install_MCBXAB_errors.madx";    ! adding multipole correctors in IR1/5 (for errors)

!Cycling w.r.t. to IP3 (mandatory to find closed orbit in collision in the presence of errors)
if (mylhcbeam<3){
  seqedit,sequence=lhcb1;flatten;cycle,start=IP3;flatten;endedit;
};
seqedit,sequence=lhcb2;flatten;cycle,start=IP3;flatten;endedit;

!********************************************

!NRJ= 450.0  ; ! injection
!NRJ= 7000.0 ; ! collision
NRJ=7000.0;

! A Laundau octupole current 20A inj, -570A col
I_MO=-300; !-300; -570;


!General switch to select collision (0/1)
ON_COLLISION:=1;
!General switch to install bb lens (0/1)
ON_BB_SWITCH:=1;
!General switch to install wire
wire_type=1;
if (wire_type==0){ ON_WIRE:=0; } else { ON_WIRE:=1; };

b_t_dist :=25.;  !bunch separation [ns]
!if (not_a_mask==1){ emittance_norm := 2.5e-6;Nb_0:=2.2E11; } else { emittance_norm := 2.50*1.0E-6; Nb_0:=1.2E11; };
if (not_a_mask==1){ emittance_norm := (2.5)*1.0e-6;Nb_0:=1.2e11; } else { emittance_norm := (2.5)*1.0e-6; Nb_0:=1.2e11; };

sigt_col=0.075; ! bunch length [m] in collision

gamma_rel      := NRJ/pmass;
epsx:=emittance_norm /gamma_rel;
epsy:=emittance_norm /gamma_rel;

!********************************************

tsplit= 2.0;   !new tune split
qx00  = 62.0;  !integer h tune
qy00  = 60.0;  !integer v tune


if (NRJ<4999.9999){
  !Inj. optics in thin lens
  call,file="slhc/opt_inj_thin.madx";  ! beta* [m]=6/10/6/10 in IR1/2/5/8
  qx0 = 62.28;   qy0 = 60.31;  qprime = 3.0;
  if (mylhcbeam<3){
    Beam,particle=proton,sequence=lhcb1,energy=NRJ,sigt=0.130,bv=1,NPART=Nb_0,sige=4.5e-4,ex=epsx,ey=epsy;
  };
  Beam,particle=proton,sequence=lhcb2,energy=NRJ,sigt=0.130,bv=bvaux,NPART=Nb_0,sige=4.5e-4,ex=epsx,ey=epsy;
};

if (NRJ>5000.0000){

beta_star_ip15=15;

if (beta_star_ip15==64){ call,file="slhc/squeeze2/opt_squeeze_640_3000_thin.madx"; };
if (beta_star_ip15==15){ call,file="slhc/squeeze2/opt_150_150_150_150_thin.madx"; };
                                                                                                                  

  qx0 = 62.315; !62.3100;   
  qy0 = 60.320; !60.3200;  
  qprime = 15; !15.0;
  ON_QPP     :=0;  ! Correction of residual Q'' by MO's
  ON_DISP    :=1;  ! Correction of spurious dispersion
  if (mylhcbeam<3){
    Beam,particle=proton,sequence=lhcb1,energy=NRJ,sigt=sigt_col,bv=1,
        NPART=Nb_0,sige=1.1e-4,ex=epsx,ey=epsy;
  };
  Beam,particle=proton,sequence=lhcb2,energy=NRJ,sigt=sigt_col,bv=bvaux,
      NPART=Nb_0,sige=1.1e-4,ex=epsx,ey=epsy;
};


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!   Rematch IP1 IP5 phase
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
! nominal round: mux_ip15 = 31.21038468 ; muy_ip15 = 30.37288037 ;
! nominal injection: mux_ip15 = 31.19538482 ; muy_ip15 = 30.36788031 ;

!mux_ip15 = 31.21038468;
!muy_ip15 = 31.19538482;

!call,file="slhc/toolkit/make_ip15phase.madx";
!call,file=slhc/toolkit/make_ip15phase_tm.madx; ! use phase-trombone instead
!call,file=slhc/toolkit/delete_ip15phase_tm.madx; ! remove phase trombone

! test new optics
if (mylhcbeam==1){
  exec,check_ip(b1);
} else {
  exec,check_ip(b2);
};
mux_ip15_ref=table(twiss,IP1,mux)-table(twiss,IP5,mux);
muy_ip15_ref=table(twiss,IP1,muy)-table(twiss,IP5,muy);
value,mux_ip15_ref,muy_ip15_ref;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!Set crossing angle and separations knobs
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
phi_IR1 =   90.000; phi_IR5 =   0.000; ! For round and flat optics
!phi_IR1 =    0.000; phi_IR5 =   90.000; ! For flathv optics
!if (not_a_mask==1){ xing15=255; } else { xing15=250; };
if (not_a_mask==1){ xing1 = 250; xing5 = 250; } else { xing1 = 250; xing5 = 250; };
sep15=0.75;
xing8=-250; sep8=-2; on_lhcb=+1;
xing2= 170; sep2=2; on_alice=1;

on_crab1=-190; on_crab5=-190;
!Avoid crabbing more than the crossing angle
!if ( abs(on_crab1)>abs(xing15) && on_crab1 <> 0) {on_crab1 = abs(on_crab1)/on_crab1 * abs(xing15);}
!if ( abs(on_crab5)>abs(xing15) && on_crab5 <> 0) {on_crab5 = abs(on_crab5)/on_crab5 * abs(xing15);}
if ( abs(on_crab1)>abs(xing1) && on_crab1 <> 0) {on_crab1 = abs(on_crab1)/on_crab1 * abs(xing1);}
if ( abs(on_crab5)>abs(xing5) && on_crab5 <> 0) {on_crab5 = abs(on_crab5)/on_crab5 * abs(xing5);}

on_x1=xing1;
on_sep1=sep15;
on_x5=xing5;
on_sep5=sep15;
on_x2=xing2; !removing alice=1 from pyIP2b1
on_sep2=sep2;
on_x8=xing8; !!removing on_lhcb from pxIP8b1
on_sep8=sep8;
value,on_x1,on_sep1,on_x5,on_sep5,on_x2,on_sep2,on_x8,on_sep8;

on_disp=1;

exec, crossing_save;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!                        TEST the optics
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!/*

exec, crossing_disable;

system,"rm -f twiss.b1";system,"rm -f twiss.b2";

if (mylhcbeam<3){
  Use, sequence=lhcb1;
  select,flag=twiss,clear;
  select, flag=twiss, pattern="IP1",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP2",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP5",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP8",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  twiss,sequence=lhcb1,file=twiss.b1;system,"cat twiss.b1";
};

Use, sequence=lhcb2;
select,flag=twiss,clear;
select, flag=twiss, pattern="IP1",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP2",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP5",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP8",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
twiss, sequence=lhcb2,file=twiss.b2;system,"cat twiss.b2";

system,"rm -f twiss.b1";system,"rm -f twiss.b2";
!Switch on Xscheme
exec,crossing_restore;

if (mylhcbeam<3){
  Use, sequence=lhcb1;
  select,flag=twiss,clear;
  select, flag=twiss, pattern="IP1",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP2",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP5",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP8",   column=name,s,x,y,px,py;
  twiss, sequence=lhcb1, file=twiss.b1;system,"cat twiss.b1";
};

xnom1=table(twiss,IP1,x);pxnom1=table(twiss,IP1,px);ynom1=table(twiss,IP1,y);pynom1=table(twiss,IP1,py);
xnom2=table(twiss,IP2,x);pxnom2=table(twiss,IP2,px);ynom2=table(twiss,IP2,y);pynom2=table(twiss,IP2,py);
xnom5=table(twiss,IP5,x);pxnom5=table(twiss,IP5,px);ynom5=table(twiss,IP5,y);pynom5=table(twiss,IP5,py);
xnom8=table(twiss,IP8,x);pxnom8=table(twiss,IP8,px);ynom8=table(twiss,IP8,y);pynom8=table(twiss,IP8,py);

Use, sequence=lhcb2;
select,flag=twiss,clear;
select, flag=twiss, pattern="IP1",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP2",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP5",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP8",   column=name,s,x,y,px,py;
twiss, sequence=lhcb2, file=twiss.b2;system,"cat twiss.b2";

value,xnom1,xnom2,xnom5,xnom8;
value,ynom1,ynom2,ynom5,ynom8;
value,pxnom1,pxnom2,pxnom5,pxnom8;
value,pynom1,pynom2,pynom5,pynom8;


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!     Xscheme definition   & Beam-beam encounters installation
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
call,file="slhc/beambeam2/macro_bb.madx";                  ! macros for beam-beam


n_insideD1 = 5;  !5;    !default value for the number of additionnal parasitic encounters inside D1

nho_IR1= 11;        ! number of slices for head-on in IR1 (between 0 and 201)
nho_IR2= 11;        ! number of slices for head-on in IR2 (between 0 and 201)
nho_IR5= 11;        ! number of slices for head-on in IR5 (between 0 and 201)
nho_IR8= 11;        ! number of slices for head-on in IR8 (between 0 and 201)

exec DEFINE_BB_PARAM;  !Define main beam-beam parameters

if (ON_COLLISION==1){
  !Switch on Xscheme in precollision
  on_x1aux=on_x1;on_sep1aux=1;
  on_x2aux=on_x2;on_sep2aux=1;
  on_x5aux=on_x5;on_sep5aux=1;
  on_x8aux=on_x8;on_sep8aux=1; on_a8aux=on_a8;
  on_aliceaux=on_alice; on_lhcbaux=on_lhcb;
  on_disp:=0;
  halo1=0;halo2=5;halo5=0;halo8=0;  !halo collision at 5 sigma's in Alice
 ! number of collision/turn at IP1/2/5/8 - BCMS https://espace.cern.ch/HiLumi/WP2/Wiki/HL-LHC%20Parameters.aspx
  nco_IP1= 2592;
  nco_IP5= nco_IP1;
  nco_IP2= 2288;
  nco_IP8= 2396;
  exec LEVEL_PARALLEL_OFFSET_FOR(2e33, 8); value,halo8;
  !Redefine the on_sep's accordingly
  exec CALCULATE_XSCHEME(halo1,halo2,halo5,halo8);
  ! Saving new crossing scheme with separation
  on_disp=on_dispaux; ! reset on_disp before saving
  exec, crossing_save;
};

on_disp=0; !more precise angles at IPs

!Record the nominal IP position and crossing angle
if(mylhcbeam==1) {use,  sequence=lhcb1;};
if(mylhcbeam>1) {use,  sequence=lhcb2;};
twiss;
xnom1=table(twiss,IP1,x);pxnom1=table(twiss,IP1,px);ynom1=table(twiss,IP1,y);pynom1=table(twiss,IP1,py);
xnom2=table(twiss,IP2,x);pxnom2=table(twiss,IP2,px);ynom2=table(twiss,IP2,y);pynom2=table(twiss,IP2,py);
xnom5=table(twiss,IP5,x);pxnom5=table(twiss,IP5,px);ynom5=table(twiss,IP5,y);pynom5=table(twiss,IP5,py);
xnom8=table(twiss,IP8,x);pxnom8=table(twiss,IP8,px);ynom8=table(twiss,IP8,y);pynom8=table(twiss,IP8,py);
value,xnom1,xnom2,xnom5,xnom8;
value,ynom1,ynom2,ynom5,ynom8;
value,pxnom1,pxnom2,pxnom5,pxnom8;
value,pynom1,pynom2,pynom5,pynom8;

if( ON_BB_SWITCH == 1){
  !Install b-b marker
  exec INSTALL_BB_MARK(b1);exec INSTALL_BB_MARK(b2);

  !Define bb lenses for both beams in all IR's and calculate # of encounters before D1
  exec CALCULATE_BB_LENS;

  !Install bb lenses
  on_ho1 = 1; on_lr1l = 1; on_lr1r = 1; npara_1 = npara0_1 + n_insideD1;
  on_ho5 = 1; on_lr5l = 1; on_lr5r = 1; npara_5 = npara0_5 + n_insideD1;
  on_ho2 = 0; on_lr2l = 0; on_lr2r = 0; npara_2 = npara0_2 + n_insideD1;
  on_ho8 = 0; on_lr8l = 0; on_lr8r = 0; npara_8 = npara0_8 + n_insideD1;
  if(mylhcbeam==1) {exec INSTALL_BB_LENS(b1);};
  if(mylhcbeam>1) {exec INSTALL_BB_LENS(b2);};

  !Print the lenses in bb_lenses.dat
  exec, PRINT_BB_LENSES;

  ON_BB_CHARGE := 0; !Switch off the charge the bb lenses

  !Install Crab Cavities for the weak beam
  call,file="slhc/toolkit/enable_crabcavities.madx";

  /*
  !Plot b-b separation
  exec PLOT_BB_SEP(1,$npara_1);
  exec PLOT_BB_SEP(2,$npara_2);
  exec PLOT_BB_SEP(5,$npara_5);
  exec PLOT_BB_SEP(8,$npara_8);
  */

  !/*
  !if (mylhcbeam<3){ seqedit,sequence=lhcb1;flatten;cycle,start=IP1;endedit; };
  !seqedit,sequence=lhcb2;flatten;cycle,start=IP1;endedit;

  if (mylhcbeam<3){
    use,sequence=lhcb1;
    select,flag=twiss,clear;
    select,flag=twiss,class=marker,pattern=PAR.*L1,range=mbxf.4l1..4/IP1.L1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    select,flag=twiss,class=marker,pattern=PAR.*L5,range=mbxf.4l5..4/IP5,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    select,flag=twiss,class=marker,pattern=PAR.*R1,range=IP1/mbxf.4r1..1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    select,flag=twiss,class=marker,pattern=PAR.*R5,range=IP5/mbxf.4r5..1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    select,flag=twiss,class=marker,pattern=IP1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    select,flag=twiss,class=marker,pattern=IP5,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
    twiss,file=twiss_bb.b1;system,"cat twiss_bb.b1";
  };

  use,sequence=lhcb2;
  select,flag=twiss,clear;
  select,flag=twiss,class=marker,pattern=PAR.*L1,range=mbxf.4l1..4/IP1.L1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  select,flag=twiss,class=marker,pattern=PAR.*L5,range=mbxf.4l5..4/IP5,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  select,flag=twiss,class=marker,pattern=PAR.*R1,range=IP1/mbxf.4r1..1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  select,flag=twiss,class=marker,pattern=PAR.*R5,range=IP5/mbxf.4r5..1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  select,flag=twiss,class=marker,pattern=IP1,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  select,flag=twiss,class=marker,pattern=IP5,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  twiss,file=twiss_bb.b2;system,"cat twiss_bb.b2";

  if(mylhcbeam==1) {use,sequence=lhcb1;};
  if(mylhcbeam>1) {use,sequence=lhcb2;};

  select,flag=twiss,clear;
  select,flag=twiss,pattern=HO,class=beambeam,column=s,name,betx,bety,alfx,alfy,mux,muy,x,y,px,py;
  twiss,file=twiss_bb;system,"cat twiss_bb";

  !if (mylhcbeam<3){ seqedit,sequence=lhcb1;flatten;cycle,start=IP3;endedit; };
  !seqedit,sequence=lhcb2;flatten;cycle,start=IP3;endedit;
  !*/

  !Remove bb markers
  exec REMOVE_BB_MARKER;

  /*
  !Make and plot footprint (at 6 sigmas)
  ON_BB_CHARGE := 1;

  !Switch on Xscheme
  exec, crossing_restore;

  nsigmax=6;

  if(qx0-qx00<0.3){
    if(mylhcbeam==1) {exec MAKEFOOTPRINT(b1);exec PLOTFOOTPRINT(b1,0.2795,0.2805,0.3095,0.3105);};
    if(mylhcbeam>1) {exec MAKEFOOTPRINT(b2);exec PLOTFOOTPRINT(b2,0.2795,0.2805,0.3095,0.3105);};
  };
  if(qx0-qx00>0.3){
    if(mylhcbeam==1) {exec MAKEFOOTPRINT(b1);exec PLOTFOOTPRINT(b1,0.300,0.315,0.310,0.325);};
    if(mylhcbeam>1) {exec MAKEFOOTPRINT(b2);exec PLOTFOOTPRINT(b2,0.300,0.315,0.310,0.325);};
  };
  ON_BB_CHARGE := 0;
  on_x1=0;on_sep1=0;
  on_x2=0;on_sep2=0;
  on_x5=0;on_sep5=0;
  on_x8=0;on_sep8=0; on_a8=0;
  on_alice=0; on_lhcb=0;
  on_disp=0;
  */

  !Remove bb lens for both beams
  !exec REMOVE_BB_LENS;
};


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!     Wire installation
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;

if (mylhcbeam==1) { use,sequence=lhcb1; } else { use,sequence=lhcb2; };

!install markers for dump

!## IP3 ##!
ip3_dump_sta.b1: marker;
ip3_dump_mid.b1: marker;
ip3_dump_end.b1: marker;

use, sequence=lhcb1;
seqedit, sequence=lhcb1;
flatten;

install, element=ip3_dump_sta.b1, at=0, from=ip3;
install, element=ip3_dump_mid.b1, at=0, from=ip3;
install, element=ip3_dump_end.b1, at=0, from=ip3;

flatten;
endedit;
use, sequence=lhcb1;


if(ON_WIRE==1){

    print, text="Starting installation of wire in lhcb1";
    call, file="macro/wire_comp_macro_lhcb1.madx";



    !## IP1 ##!


! Wire compensator current in [q*c*Nb] for a wire 1m long

    I_wire_c_l1.b1 = (26.84233900);   ! in [q*c*Nb] for a wire 1m long. The plus (+) sign is for the compensation of the BBLR kicks if the wires are placed insite (use of electrons in the wire). The minus (-) is for no compensation (use of protons in the wire).
    I_wire_c_r1.b1 = (26.45635300);    ! in [q*c*Nb] for a wire 1m long. The plus (+) sign is for the compensation of the BBLR kicks if the wires are placed insite (use of electrons in the wire). The minus (-) is for no compensation (use of protons in the wire).

    if (I_wire_c_l1.b1==0){ swich_for_l1_wc_install=0; } else { swich_for_l1_wc_install=1; };
    if (I_wire_c_r1.b1==0){ swich_for_r1_wc_install=0; } else { swich_for_r1_wc_install=1; };
    

! wire compensator longitudinal and transverse position 

    wire_c_s_l1.b1 = -195; 	! The s position of the left wire compensator from the IP1 in [m] / =0 if no wire will installed.
    wire_c_s_r1.b1 = 195; 	! The s position of the right wire compensator from the IP1 in [m] / =0 if no wire will installed.
    wire_c_x_l1.b1 = (0.0);  	! The x distance of the left wire compensator from the weak beam in [m]. For up placement (outside of the crossing plain) use the plus (+) sing and for down placement (insite of the crossing plain) use the minus (-) sing.
    wire_c_y_l1.b1 = (0.00800000);  	! The y distance of the left wire compensator from the weak beam in [m]. For insite placement (between the weak and the strong beam) use the plus (+) sing and for outsite placement use the minus (-) sing.
    wire_c_x_r1.b1 = (0.0);  	! The x distance of the right wire compensator from the weak beam in [m]. For up placement (outside of the crossing plain) use the plus (+) sing and for down placement (insite of the crossing plain) use the minus (-) sing.
    wire_c_y_r1.b1 = (0.00800000);  	! The y distance of the right wire compensator from the weak beam in [m]. For insite placement (between the weak and the strong beam) use the plus (+) sing and for outsite placement use the minus (-) sing.  


    !## IP5 ##!


! Wire compensator current in [q*c*Nb] for a wire 1m long

    I_wire_c_l5.b1 = (26.84247000);    ! in [q*c*Nb] for a wire 1m long. The plus (+) sign is for the compensation of the BBLR kicks if the wires are placed insite (use of electrons in the wire). The minus (-) is for no compensation (use of protons in the wire).
    I_wire_c_r5.b1 = (26.45646200);    ! in [q*c*Nb] for a wire 1m long. The plus (+) sign is for the compensation of the BBLR kicks if the wires are placed insite (use of electrons in the wire). The minus (-) is for no compensation (use of protons in the wire).

    if (I_wire_c_l5.b1==0){ swich_for_l5_wc_install=0; } else { swich_for_l5_wc_install=1; };
    if (I_wire_c_r5.b1==0){ swich_for_r5_wc_install=0; } else { swich_for_r5_wc_install=1; };


! wire compensator longitudinal and transverse position

    wire_c_s_l5.b1 = -195;  	! The s position of the left wire compensator from the IP5 in [m] / =0 if no wire will installed.
    wire_c_s_r5.b1 = 195;  	! The s position of the right wire compensator from the IP5 in [m] / =0 if no wire will installed.
    wire_c_x_l5.b1 = (0.00800000);   ! The x distance of the left wire compensator from the weak beam in [m]. For insite placement (between the weak and the strong beam) use the plus (+) sing and for outsite placement use the minus (-) sing.
    wire_c_y_l5.b1 = (0.0);   ! The y distance of the left wire compensator from the weak beam in [m]. For up placement (outside of the crossing plain) use the plus (+) sing and for down placement (insite of the crossing plain) use the minus (-) sing.
    wire_c_x_r5.b1 = (0.00800000);   ! The x distance of the right wire compensator from the weak beam in [m]. For insite placement (between the weak and the strong beam) use the plus (+) sing and for outsite placement use the minus (-) sing.
    wire_c_y_r5.b1 = (0.0);   ! The y distance of the left wire compensator from the weak beam in [m]. For up placement (outside of the crossing plain) use the plus (+) sing and for down placement (insite of the crossing plain) use the minus (-) sing.



    if (wire_type==1) {
    
        ! On-off wire compensator
        
        !## IP1 ##!
        on_off_w_c_l1.b1 := 1; ! Wire compensator on  =1 / off =0
        on_off_w_c_r1.b1 := 1; ! Wire compensator on  =1 / off =0
        !## IP5 ##!
        on_off_w_c_l5.b1 := 1; ! Wire compensator on  =1 / off =0
        on_off_w_c_r5.b1 := 1; ! Wire compensator on  =1 / off =0
   
        exec, INSTALL_MADX_WIRE_COMP_LHCB1;
        
        !## IP1 ##!
        on_off_w_c_l1.b1 := 0;
        on_off_w_c_r1.b1 := 0;
        !## IP5 ##!
        on_off_w_c_l5.b1 := 0;
        on_off_w_c_r5.b1 := 0;
	};
	
	if (wire_type==2) { exec, INSTALL_SIXTRACK_WIRE_COMP_LHCB1;};
   

}; ! end of installation


        



!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!               Prepare nominal twiss tables
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
if (mylhcbeam==1) { use,sequence=lhcb1; } else { use,sequence=lhcb2; };

/*
 twiss;
 x.ip1=table(twiss,IP1,x);px.ip1=table(twiss,IP1,px);
 y.ip1=table(twiss,IP1,y);px.ip1=table(twiss,IP1,py); //irrelevant when recycling the sequences
*/


 twiss, table=nominal;   // used by orbit correction
 beta.ip1=table(twiss,IP1,betx);value,beta.ip1;

! print nominal optics parameter at the MB, MQS and MSS for
! b2, b3, b4, b5, a2 and a3 correction
 select, flag=twiss, clear;
 select, flag=twiss,pattern=MB\.   ,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MBH\.   ,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.14,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.15,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.16,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.17,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.18,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.19,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.20,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,pattern=MQT\.21,class=multipole,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,class=MQS                      ,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,class=MSS                      ,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,class=MCO                      ,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,class=MCD                      ,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 select, flag=twiss,class=MCS                      ,column=name,k0L,k1L,betx,bety,dx,mux,muy;
 twiss,  file='temp/optics0_MB.mad';

! print nominal optics parameter at the D1, MQX and triplet corrector
! for triplet correction
 select, flag=twiss, clear;
 select, flag=twiss, pattern=MQX  , class=multipole, column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MBX  , class=multipole, column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MBRC , class=multipole, column=name,betx,bety,x,y;
 if (correct_for_D2==1){
   select, flag=twiss, pattern=MBRD , class=multipole, column=name,betx,bety,x,y;
 };
 select, flag=twiss, pattern=MQSX                  , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCQSX                 , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCSX                  , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCTX                  , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCOSX                 , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCOX                  , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCSSX                 , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCDX                  , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCDSX                 , column=name,betx,bety,x,y;
 select, flag=twiss, pattern=MCTSX                 , column=name,betx,bety,x,y;
 if (correct_for_MCBX==1){
   select, flag=twiss, pattern=MCBXF, class=multipole, column=name,betx,bety,x,y;
 };
 twiss,  file='temp/optics0_inser.mad';

! disable crossing bumps
exec, crossing_disable;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!                   Align separation magnets
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!if(mylhcbeam==1){
!  call, file = "db5/measured_errors/align_D1_slices.b1.madx";
!  call, file = "db5/measured_errors/align_D2_slices.b1.madx";
!  call, file = "db5/measured_errors/align_D3_slices.b1.madx";
!  call, file = "db5/measured_errors/align_D4_slices.b1.madx";
!};
!
!if(mylhcbeam>1){
!  call, file = "db5/measured_errors/align_D1_slices.b2.madx";
!  call, file = "db5/measured_errors/align_D2_slices.b2.madx";
!  call, file = "db5/measured_errors/align_D3_slices.b2.madx";
!  call, file = "db5/measured_errors/align_D4_slices.b2.madx";
!};

call,file="slhc/toolkit/align_sepdip.madx";
!exec,align_mbxw;  ! V6.503  D1
!exec,align_mbrc15;! V6.503 D2 in IR15
exec,align_mbx15; ! HL-LHC D1
exec,align_mbrd15;! HL-LHC D2 in IR15

exec,align_mbx28; ! V6.503 D1 in IR28
exec,align_mbrc28;! V6.503 D2 in IR28
exec,align_mbrs;  ! V6.503 D3 in IR4
exec,align_mbrb;  ! V6.503 D4 in IR4

call,file="slhc/toolkit/align_mbh.madx"; ! align 11T dipoles


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!         Call error subroutines (nominal machine and new IT/D1)
!                         and error tables
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

! Error routine and measured error table for nominal LHC

call,file="db5/measured_errors/Msubroutines_new.madx";
call,file="db5/measured_errors/Msubroutines_MS_MSS_MO_new.madx";
call,file="db5/toolkit/Orbit_Routines.madx";
call,file="slhc/errors/SelectLHCMonCor.madx";
readtable, file="db5/measured_errors/rotations_Q2_integral.tab";

! Error routine and error table for new IT/D1/D2/Q4/Q5
useMQXFAB=0;

call,file="slhc/errors/macro_error.madx";   ! macros for error generation in the new IT/D1's

if (useMQXFAB) {
  call,file="slhc/errors/ITa_errortable_v5"; ! target error table for the new IT
  call,file="slhc/errors/ITb_errortable_v5"; ! target error table for the new IT
} else {
  call,file="slhc/errors/ITbody_errortable_v5"; ! target error table for the new IT
  call,file="slhc/errors/ITnc_errortable_v5"; ! target error table for the new IT
  call,file="slhc/errors/ITcs_errortable_v5"; ! target error table for the new IT
};
call,file="slhc/errors/D1_errortable_v1"; ! target error table for the new D1

call,file="slhc/errors/D2_errortable_v5"; ! target error table for the new D2
if (correct_for_D2==1){
  call,file="slhc/errors/D2_empty_errortable"; ! We are only correcting the b3 errors, put the other errors to zero
};

call,file="slhc/errors/Q4_errortable_v2";! target error table for the new Q4 in IR1 and IR5
!value,Rr_MQXCD,Rr_MBXAB,Rr_MBRD,Rr_MQYY,Rr_MQYL;
!a2R_MQXCD_inj:=20.00;a2R_MQXCD_col:=20.00;! up to 3 mrad roll of the new MQX's (1 mrad r.m.s)

!b5M_MQXCD_col  :=  0.0000 ; b5U_MQXCD_col  :=  0.4200 ; b5R_MQXCD_col  :=  0.4200 ; !..Errors as in IT_errortable_v3
!a5M_MQXCD_col  :=  0.0000 ; a5U_MQXCD_col  :=  0.4300 ; a5R_MQXCD_col  :=  0.4300 ; !..Errors as in IT_errortable_v3
!a6M_MQXCD_col  :=  0.0000 ; a6U_MQXCD_col  :=  0.3100 ; a6R_MQXCD_col  :=  0.3100 ; !..Errors as in IT_errortable_v3

! MCBX effect Disabled for the time being
!call,file="slhc/errors/MCBXFAB_errortable_v1";
!call,file="slhc/errors/MBH_errortable_v1";

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!                Switch ON/OFF some multipole
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

!exec ON_ALL_MULT;
!ON_LSF=1;

ON_A1s =  0 ; ON_A1r =  0 ; ON_B1s =  0 ; ON_B1r =  0;
ON_A2s =  0 ; ON_A2r =  0 ; ON_B2s =  0 ; ON_B2r =  0;
ON_A3s =  1 ; ON_A3r =  1 ; ON_B3s =  1 ; ON_B3r =  1;
ON_A4s =  1 ; ON_A4r =  1 ; ON_B4s =  1 ; ON_B4r =  1;
ON_A5s =  1 ; ON_A5r =  1 ; ON_B5s =  1 ; ON_B5r =  1;
ON_A6s =  1 ; ON_A6r =  1 ; ON_B6s =  1 ; ON_B6r =  1;
ON_A7s =  1 ; ON_A7r =  1 ; ON_B7s =  1 ; ON_B7r =  1;
ON_A8s =  1 ; ON_A8r =  1 ; ON_B8s =  1 ; ON_B8r =  1;
ON_A9s =  1 ; ON_A9r =  1 ; ON_B9s =  1 ; ON_B9r =  1;
ON_A10s =  1; ON_A10r =  1; ON_B10s =  1; ON_B10r =  1;
ON_A11s =  1; ON_A11r =  1; ON_B11s =  1; ON_B11r =  1;
ON_A12s =  ON_LSF; ON_A12r = ON_LSF; ON_B12s = ON_LSF; ON_B12r =  ON_LSF;
ON_A13s =  ON_LSF; ON_A13r = ON_LSF; ON_B13s = ON_LSF; ON_B13r =  ON_LSF;
ON_A14s =  ON_LSF; ON_A14r = ON_LSF; ON_B14s = ON_LSF; ON_B14r =  ON_LSF;
ON_A15s =  ON_LSF; ON_A15r = ON_LSF; ON_B15s = ON_LSF; ON_B15r =  ON_LSF;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
! Define the seed number (for statistical error assignment in the new IT/D1)
! and call the corresponding measured error table for nominal LHC magnets
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

!if (not_a_mask==1){
!  if (NRJ<4999.999) {readtable, file="wise/injection_errors-emfqcs-1.tfs" ;};
!  if (NRJ>5000.000) {readtable, file="wise/collision_errors-emfqcs-1.tfs" ;};
!} else {
!  if (NRJ<4999.999) {readtable, file="wise/injection_errors-emfqcs-%SEEDRAN.tfs" ;};
!  if (NRJ>5000.000) {readtable, file="wise/collision_errors-emfqcs-%SEEDRAN.tfs" ;};
!};

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!             apply field errors to MB magnets
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!call,   file="db5/measured_errors/Efcomp_MB.madx"  ;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!     correct orbit distortion resulting from MB magnets
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
if((ON_A1S)^2+(ON_A1r)^2+(ON_B1S)^2+(ON_B1r)^2 >0){
  exec,   initial_micado(4);
  exec,   initial_micado(4);
};



!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
! MO settings
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

brho:=NRJ*1e9/clight;
if (mylhcbeam==1){
  KOF.A12B1:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A23B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A34B1:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A45B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A56B1:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A67B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A78B1:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A81B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A12B1:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A23B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A34B1:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A45B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A56B1:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A67B1:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A78B1:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A81B1:=Kmax_MO*I_MO/Imax_MO/brho;
};

if (mylhcbeam>1){
  KOF.A12B2:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A23B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A34B2:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A45B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A56B2:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A67B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOF.A78B2:=Kmax_MO*I_MO/Imax_MO/brho; KOF.A81B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A12B2:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A23B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A34B2:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A45B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A56B2:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A67B2:=Kmax_MO*I_MO/Imax_MO/brho;
  KOD.A78B2:=Kmax_MO*I_MO/Imax_MO/brho; KOD.A81B2:=Kmax_MO*I_MO/Imax_MO/brho;
};


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!               correction of field errors in MB (compatible with V6.503 & SLHC)
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 select, flag=error, clear;
 select, flag=error, pattern=MB\.,class=multipole;
 select, flag=error, pattern=MBH\.,class=multipole;
 esave,  file="temp/MB.errors";
 system, "slhc/errors/corr_MB_ats_v4";
 call,   file="temp/MB_corr_setting.mad";
!! exec reset_MB_corr;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
! correction of triplet and D1 (only in collision, not compatible V6.503)
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
if (NRJ>5000.0000){
  option, echo, warn, -info;
  select, flag=error, clear;
  select, flag=error, pattern=MQX,  class=multipole;
  select, flag=error, pattern=MBX,  class=multipole;
  !select, flag=error, pattern=MBRC, class=multipole;
  if (correct_for_D2==1){
    select, flag=error, pattern=MBRD, class=multipole;
  };
  if (correct_for_MCBX==1){
    select, flag=error, pattern=MCBXF, class=multipole;
  };
  esave,  file="temp/tripD1D2.errors";
  system, "slhc/errors/corr_tripD1_v6";
  call,   file="temp/MCX_setting.mad";
  !kcsx3.l1:=0 ;kcsx3.r1:=0 ;kcsx3.l5:=0 ;kcsx3.r5:=0 ; !switch off b3  correction in IR1 and IR5
  !kcox3.l1:=0 ;kcox3.r1:=0 ;kcox3.l5:=0 ;kcox3.r5:=0 ; !switch off b4  correction in IR1 and IR5
  !kcdx3.l1:=0 ;kcdx3.r1:=0 ;kcdx3.l5:=0 ;kcdx3.r5:=0 ; !switch off b5  correction in IR1 and IR5
  !kctx3.l1:=0 ;kctx3.r1:=0 ;kctx3.l5:=0 ;kctx3.r5:=0 ; !switch off b6  correction in IR1 and IR5
  !kqsx3.l1:=0 ;kqsx3.r1:=0 ;kqsx3.l5:=0 ;kqsx3.r5:=0 ; !switch off a2  correction in IR1 and IR5
  !kcssx3.l1:=0;kcssx3.r1:=0;kcssx3.l5:=0;kcssx3.r5:=0; !switch off a3  correction in IR1 and IR5
  !kcosx3.l1:=0;kcosx3.r1:=0;kcosx3.l5:=0;kcosx3.r5:=0; !switch off a4  correction in IR1 and IR5
  !kcdsx3.l1:=0;kcdsx3.r1:=0;kcdsx3.l5:=0;kcdsx3.r5:=0; !switch off a5  correction in IR1 and IR5
  !kctsx3.l1:=0;kctsx3.r1:=0;kctsx3.l5:=0;kctsx3.r5:=0; !switch off a6  correction in IR1 and IR5

  kcsx3.l2 :=0;kcsx3.r2 :=0;kcsx3.l8 :=0;kcsx3.r8 :=0; !switch off b3  correction in IR2 and IR8
  kcox3.l2 :=0;kcox3.r2 :=0;kcox3.l8 :=0;kcox3.r8 :=0; !switch off b4  correction in IR2 and IR8
  kctx3.l2 :=0;kctx3.r2 :=0;kctx3.l8 :=0;kctx3.r8 :=0; !switch off b6  correction in IR2 and IR8
  kqsx3.l2 :=0;kqsx3.r2 :=0;kqsx3.l8 :=0;kqsx3.r8 :=0; !switch off a2  correction in IR2 and IR8
  kcssx3.l2:=0;kcssx3.r2:=0;kcssx3.l8:=0;kcssx3.r8:=0; !switch off a3  correction in IR2 and IR8
  kcosx3.l2:=0;kcosx3.r2:=0;kcosx3.l8:=0;kcosx3.r8:=0; !switch off a4  correction in IR2 and IR8

  !exec reset_MQX_corr;                             !switch off all IT multipole correction in all IR's
};

if (correct_for_D2==1){
  ! Remove the average errors and assign the real errors of D2
  use_average_errors_MBRD=0;
  call,file="slhc/errors/D2_errortable_v5";
  eoption,add=false;
  eoption,seed=myseed+103;
  call, file="slhc/errors/Efcomp_MBRD.madx";
};

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!    correct orbit distortion resulting from other magnets
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
if((ON_A1S)^2+(ON_A1r)^2+(ON_B1S)^2+(ON_B1r)^2 >0){
  exec, initial_micado(4);
  exec, initial_micado(4);
  exec, initial_micado(4);
  exec, initial_micado(4);
};

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!    final orbit correction before applying crossing scheme
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
if((ON_A1S)^2+(ON_A1r)^2+(ON_B1S)^2+(ON_B1r)^2 >0){
  exec, final_micado(0.004);
};


! end of orbit correction, now switch ON crossing scheme
! restoring crossing angle
on_x1=on_x1aux;on_sep1=on_sep1aux;
on_x2=on_x2aux;on_sep2=on_sep2aux;
on_x5=on_x5aux;on_sep5=on_sep5aux;
on_x8=on_x8aux;on_sep8=on_sep8aux; on_a8=on_a8aux;
on_alice=on_aliceaux; on_lhcb=on_lhcbaux;
on_disp=on_dispaux;

! coguess,x=x.ip1,px=px.ip1,y=y.ip1,py=py.ip1;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!   Fine tuning of coupling after CO correction and with Xscheme
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

call,file="slhc/errors/FineCouplingCorrection.madx";

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!                 matching of orbit, tune and chromaticity
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
if(ON_COLLISION==0) {ON_BB_CHARGE:=1;};    ! W/o head-on Q and Q' are matched with bb

!Rematch the Xscheme towards specified separation and Xange in IP1/2/5/8
call,file="slhc/toolkit/rematchCOIP.madx";
!Rematch the CO in the arc for dispersion correction
if(ON_DISP<>0) {call,file="slhc/toolkit/rematchCOarc.madx";};

if(mylhcbeam==1){
  kqtf=kqtf.b1;kqtd=kqtd.b1;kqtf.b1:=kqtf;kqtd.b1:=kqtd;
  ksf=ksf.b1;ksd=ksd.b1;ksf.b1:=ksf;ksd.b1:=ksd;
};

if(mylhcbeam>1){
  kqtf=kqtf.b2;kqtd=kqtd.b2;kqtf.b2:=kqtf;kqtd.b2:=kqtd;
  ksf=ksf.b2;ksd=ksd.b2;ksf.b2:=ksf;ksd.b2:=ksd;
};

 match;
 global, q1=qx0, q2=qy0;
 vary,   name=kqtf, step=1.0E-7 ;
 vary,   name=kqtd, step=1.0E-7 ;
 lmdif,  calls=100, tolerance=1.0E-21;
 endmatch;

 match,chrom;
 global, dq1=qprime, dq2=qprime;
 vary,   name=ksf;
 vary,   name=ksd;
 lmdif,  calls=100, tolerance=1.0E-21;
 endmatch;

 match,chrom;
 global, dq1=qprime, dq2=qprime;
 global, q1=qx0, q2=qy0;
 vary,   name=ksf;
 vary,   name=ksd;
 vary,   name=kqtf, step=1.0E-7 ;
 vary,   name=kqtd, step=1.0E-7 ;
 lmdif,  calls=500, tolerance=1.0E-21;
 endmatch;




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!## always check the following matching section ##!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!## 1->use Q4Q5 for matching / 0->use the arcs for matching ##!
local_match_wc := 1; 

!## on / off BEAM-BEAM (HO and LR) during matching ##!
ON_BB_CHARGE := 1;

! ## for beta*=15 [cm] the following tunes##!
if (on_ho1==1 && on_ho5==1) {
if (xing1==xing5) {

qx_tar = 62.3058376145667;
qy_tar = 60.3106457114245;

};
};


if (on_ho1==0 && on_ho5==0) {
qx_tar=qx0;
qy_tar=qy0;
};


if (ON_BB_CHARGE==1){

 match;
 global, q1=qx_tar, q2=qy_tar;
 vary,   name=kqtf, step=1.0E-7 ;
 vary,   name=kqtd, step=1.0E-7 ;
!vary,   name=dQx.b1_sq, step=1.0E-7 ;
!vary,   name=dQy.b1_sq, step=1.0E-7 ;
 lmdif,  calls=100, tolerance=1.0E-21;
 endmatch;

 match;
 global, q1=qx_tar, q2=qy_tar;
 vary,   name=kqtf, step=1.0E-7 ;
 vary,   name=kqtd, step=1.0E-7 ;
!vary,   name=dQx.b1_sq, step=1.0E-7 ;
!vary,   name=dQy.b1_sq, step=1.0E-7 ;
 lmdif,  calls=500, tolerance=1.0E-21;
 endmatch;
};



if(ON_WIRE==1){

 if (wire_type==1) { exec, TUNE_MATCHING_MADX_WIRE; };
 if (wire_type==2) { exec, TUNE_MATCHING_SIXTRACK_WIRE; };

};



!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!                 Check corrector strength
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

call, file="slhc/errors/corr_value.madx";

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!                 final twiss before sending to sixtrack
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


ON_BB_CHARGE:=1;

if(ON_WIRE==1 && wire_type==1){
!## IP1 ##!
on_off_w_c_l1.b1 := 1;
on_off_w_c_r1.b1 := 1;
!## IP5 ##!
on_off_w_c_l5.b1 := 1;
on_off_w_c_r5.b1 := 1;
};


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
! redefine makefootprint
MAKEFOOTPRINT(nam): macro = {
  
  small=0.02;
  big=sqrt(1.-small^2);
  track, deltap=0.000, file=track,  ONETABLE=false;
  xs=small; ys=small;
  value,xs,ys;
  start,fx=xs,fy=ys;  // zero amplitude
  n=1; // sigma multiplier
  m=0; // angle multiplier
  while (n <= 6)
  {
    angle = 15*m*pi/180;
    if (m == 0) {xs=n*big; ys=n*small;}
    elseif (m == 6) {xs=n*small; ys=n*big;}
    else
    {
      xs=n*cos(angle);
      ys=n*sin(angle);
    }
    value,xs,ys;
    start,fx=xs,fy=ys;
    m=m+1;
    if (m == 7) { m=0; n=n+1;}
  };

  !OBSERVE, PLACE="IP1";
  !run,turns=1024;
  dynap,fastune,turns=1024;
  endtrack;

  system, "rm -rf temp_f_nam";
  system, "mkdir temp_f_nam";
  write,table=dynap,file="temp_f_nam/dynap";
  write,table=dynaptune,file="temp_f_nam/dynaptune";
  !system,"/afs/cern.ch/group/si/slap/bin/foot < temp_f_nam/dynaptune > temp_f_nam/footprint";
};

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;

footkey=1;

if (footkey==1){
exec, MAKEFOOTPRINT(HL-LHC_v1d3_tune_315320_xing_250_vrf_16_dp_0d00027_io_m300_ho_15_lr_15_wc_1_wcm_1_wlbb_0_bblrm_1_tdw_0_Iw_0);
};



/* 
twiss;
 value, table(summ,q1);
 value, table(summ,q2);
 value, table(summ,dq1);
 value, table(summ,dq2);
 sixtrack, radius=0.017;
*/

!if (NRJ<4999.9999) {VRF400:=8. ;LAGRF400.B1=0.5;LAGRF400.B2=0.;};
!if (NRJ>5000.0000) {VRF400:=16.;LAGRF400.B1=0.5;LAGRF400.B2=0.;};

if (NRJ<4999.9999) {VRF400:=16 ;LAGRF400.B1=0.5;LAGRF400.B2=0.;};
if (NRJ>5000.0000) {VRF400:=16;LAGRF400.B1=0.5;LAGRF400.B2=0.;};

twiss;
xnom1=table(twiss,IP1,x);pxnom1=table(twiss,IP1,px);ynom1=table(twiss,IP1,y);pynom1=table(twiss,IP1,py);
xnom2=table(twiss,IP2,x);pxnom2=table(twiss,IP2,px);ynom2=table(twiss,IP2,y);pynom2=table(twiss,IP2,py);
xnom5=table(twiss,IP5,x);pxnom5=table(twiss,IP5,px);ynom5=table(twiss,IP5,y);pynom5=table(twiss,IP5,py);
xnom8=table(twiss,IP8,x);pxnom8=table(twiss,IP8,px);ynom8=table(twiss,IP8,y);pynom8=table(twiss,IP8,py);


value,xnom1,xnom2,xnom5,xnom8;
value,ynom1,ynom2,ynom5,ynom8;
value,pxnom1,pxnom2,pxnom5,pxnom8;
value,pynom1,pynom2,pynom5,pynom8;
value, table(summ,q1);
value, table(summ,q2);
value, table(summ,dq1);
value, table(summ,dq2);


! Printout the WIRE LENSES
if (ON_WIRE==1){

    if (wire_type==1) {
        bb_macro_version=2;
        exec, PRINT_MADX_WIRE_LENSES_IN_BB_LENSES;
	};
	if (wire_type==2) { exec, PRINT_SIXTRACK_WIRE_LENSES_IN_BB_LENSES;};
};

twiss, save;
!sixtrack,cavall, mult_auto_off,radius=0.017;
sixtrack,cavall, radius=0.017;

! Set the fc.2 for wire in sixtrack
if (ON_WIRE==1 && wire_type==2){
    exec, SET_SIXTRACK_WIRE_LENSES_IN_FC2;
};

if( ON_BB_SWITCH == 1){
  !Fix bb lenses in sixtrack input
  exec, SIXTRACK_INPUT_BB_LENSES;
};


select, flag=twiss, clear;
select, flag=twiss, pattern=MQ.;
twiss,file="last_twiss.1";

stop; 
