mydrift: drift, l=1;
qfk1 := 5.08623 + 0.05;
qdk1 := 5.08623 - 0.05;
qf: quadrupole, l=0.1, k1:= qfk1;
qd: quadrupole, l=0.1, k1:=-qdk1;



l1: line:=(qf, mydrift, qd, m1, mydrift);
m: marker;
m1: marker;

sx1_l = 0.01;
sx1_k2 = 0.2;

sx1: sextupole, l:=sx1_l, k2:=sx1_k2;

myline: line:=(l1, l1, l1, l1, m, sx1);




beam,particle=electron, energy=6500, ex=5e-5, ey=5.E-10, sigt=0.077, sige=1.1E-4;
use, period=myline;



select,flag=twiss,clear;
select, flag=twiss, column=name,s,Energy,beta11,beta22,beta33,alfa11,alfa22,mu1,mu2,mu3,disp1,disp2,
                          re11,re12,re13,re14,re15,re16,
                          re21,re22,re23,re24,re25,re26,
                          re31,re32,re33,re34,re35,re36,
                          re41,re42,re43,re44,re45,re46,
                          re51,re52,re53,re54,re55,re56,
                          re61,re62,re63,re64,re65,re66;



ptc_create_universe;
  ptc_create_layout, model=1, method=6, nst=5, exact=true, closed_layout=true;
  
!  ptc_knob, element=QFN24,   kn=0,1, exactmatch=false;
!  ptc_printframes, file="leir.C", format=rootmacro;!this prints my visualisation with ROOT

  !!!!!       T I M E   T R U E ,   T O T P A T H  F A L S E
  ptc_setswitch, debuglevel=1, exact_mis=true, time=true, totalpath=false;


  
  ptc_twiss, table=twiss, icase=5, no=4, closed_orbit=false, rmatrix, normal, writetmap, trackrdts;
  
  
  write, table=twiss, file="ptc.twiss";
  write, table=ptc_twiss_summary, file="ptc.summ.twiss";
  write, table=nonlin, file="ptc.nonlin";


  ! analitical formulas from A. Franchi https://arxiv.org/abs/1402.1461
  bx = table(twiss, m, beta11);
  by = table(twiss, m, beta22);
  qx = table(twiss, myline$end, mu1);
  qy = table(twiss, myline$end, mu2);

  na = sx1_k2*sx1_l*sqrt(bx*bx*bx);

  dc = 48*(1-cos(2*pi*3*qx));
  dd = -48*sin(2*pi*3*qx);

  f3000_de = dc*dc+dd*dd;
  f3000_re = -1*(na*dc/f3000_de);
  f3000_im = -1*(-na*dd/f3000_de);

  h3000_re = -1*na/48;


  !1200
  dc = 16*(1-cos(-2*pi*qx));
  dd = -16*sin(-2*pi*qx);
  f1200_de = dc*dc+dd*dd;
  f1200_re = -1*(na*dc/f1200_de);
  f1200_im = -1*(-na*dd/f1200_de);


  ! comparisons of PTC values with analitical 
  !    ( in fact the analitical formulas are not exact 
  !      because they neglect higher order contributions )
  ff3000_re = table(nonlin, GNFC_3_0_0_0_0_0,value);
  ff3000_im = table(nonlin, GNFS_3_0_0_0_0_0,value);

  value, f3000_re, ff3000_re, f3000_re - ff3000_re;

  value, f3000_im, ff3000_im, f3000_im - ff3000_im;



  ff1200_re = table(nonlin, GNFC_1_2_0_0_0_0,value);
  ff1200_im = table(nonlin, GNFS_1_2_0_0_0_0,value);

  value, f1200_re, ff1200_re, f1200_re - ff1200_re;
  value, f1200_im, ff1200_im, f1200_im - ff1200_im;


  hh3000_im = table(nonlin, HAMS_3_0_0_0_0_0, value);
  value, hh3000_im;

  hh3000_re = table(nonlin, HAMC_3_0_0_0_0_0, value);
  value, h3000_re, hh3000_re, h3000_re - hh3000_re;


  write, table="twissrdt",file="twissrdt.tfs";

  value, table(twissrdt,QF[2],GNFC300000);
  
  plot, table=twissrdt, haxis=s,vaxis=gnfc300000;
  
ptc_end;

