!Example file showing the usage of PTC_TRACK command 
!that sets the internal state (i.e switches) of PTC
!
!Piotr Skowronski (CERN)
!
! examples/ptc_madx_interface/intstate/intstate.ptc.madx
!
option,-echo;
option,-info;

LQL := 0.220; ! length of QL
LQS := 0.124; ! length of QS
QL  : QUADRUPOLE, L=LQL; ! QL quadrupole
QS  : QUADRUPOLE, L=LQS; ! QS quadrupole


HACD: hACdipole, L= 0.25, volt=0.001, freq=0.01, 
                 ramp1=100,
                 ramp2=200,
                 ramp3=800,
                 ramp4=900; 

VACD: vACdipole, L= 0.25, volt=0.001, freq=0.98, 
                 ramp1= 50,
                 ramp2=200,
                 ramp3=800,
                 ramp4=950; 
                 

                 

ds_ql     : drift, l=0.25;    ! short drift between quads in Daniels design
ds_trip   : drift, l=(3.34-CAVLENGTH)/2.; ! long drift between quads in Daniels design
beforecav : marker;
aftercav  : marker;
k_ls1     := -1.53125*.56/LQL; ! weaker foc.
k_ls2     :=  1.53125/LQL;     ! weaker foc.
quad_l1   : quadrupole, l=LQL,   k1=k_ls1;
quad_l2   : quadrupole, l=LQL/2, k1=k_ls2;
LDLS      :=(LQL-LQS)/2.; ! half L difference of QL and QS
dls       : DRIFT, L=LDLS; ! 0.048 m

ctfcell  : line=( quad_l2,ds_ql,quad_l1,
                  ds_trip ,
                  VACD,
                  ds_trip,
                  quad_l1,ds_ql,quad_l2, 
                  beforecav, 
                  HACD,
                  aftercav,
);

ctf   : line=(1*ctfcell);

! calculate Twiss parameters for regular cell structure
beam, PARTICLE=ELECTRON, ENERGY=5d0;

USE, period=ctf;


ptc_create_universe;
  ptc_create_layout, model=1, method=2, nst=5, exact=false, closed_layout=false;

  ptc_start, x=0, px=0, y=0, py=0, pt=0, t=0.0;


  ptc_setswitch, debuglevel=1, maxacceleration=false, exact_mis=true, time=true, totalpath=false, fringe=true, modulation=true;

  ptc_track, icase=6, element_by_element,  dump, maxaper={1e6,1e6,1e6,1e6,1e6,1e6}, turns=1000, ffile=1;

  ptc_track_end;

ptc_end;


!renamefile, file="trackone", to="trackoneLag0";



stop;
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  SSSSS  TTTTT   OOO   PPP
!  S        T    O   O  P  P
!  SSSSS    T    O   O  PPP
!      S    T    O   O  P
!  SSSSS    T     OOO   P
!
