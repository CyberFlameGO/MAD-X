
/******************************************************************************************
 *
 * PS BOOSTER. Directory: /afs/cern.ch/eng/ps/cps/Psb
 * It consists of 16 identical periods, apart from the equipment
 * in the straight sections.
 * The BOOSTER has a radius of 25m and thus a circonference of 157.08m
 *
 * The injected beam from the linac have Ekin=50 MeV proton beam with p~300MeV/c.
 * (311 MeV/c according to the report: http://accelconf.web.cern.ch/accelconf/p05/PAPERS/TPAT054.PDF)
 * corresponding to E=0.988471 GeV and E0=0.938272 GeV. (The rest mass of a proton is 0.938272 GeV)
 *
 * The kinetic energy of the extracted beam is Ekin=1.4 GeV and p=2.141766 GeV/c [p=(SQRT(Ekin^2+2*E0*Ekin)/c]
 * The beam is extracted to the PS. In the PS, it is injected in SS42.
 *
 *
 * Revolution time at extraction:  T,revolution,ext = 572.79 ns (nano  seconds)
 * Revolution time at injection:   T,revolution,inj =   1.6  us (micro seconds)
 * The BOOSTER today has 2 bunches in each ring, i.e. 8 bunches in the PS.
 * Injection time: C=275 ms, measure time C=290 ms
 *
 * Working points:
 * Injection - high intensity          : QX=4.28,  QY=4.55
 * Injection - low  intensity e.g. LHC : QX=4.28,  QY=4.45
 * Extraction                          : QX=4.172, QY=4.23   = wp1
 *
 *
 *
 *
 * The 2 MHz cavities are for the H0 mode, they are run around 8kV.
 * They are run at the revolution frequency.
 * The beam is below transition.
 *
 * The 4 MHz cavities are used to flatten the bunches.
 * They reduce the peak current compared to the average currents.
 * The 4 MHz cavities are run between 6-8 kV.
 * They are run at twice the revolution frequency.
 *
 *
 * All elements in the straight sections added.  21 Dec 2006 O.Berrig
 * New working point: QX = 4.172 and QY = 4.23.  21 Dec 2006 O.Berrig
 *
 *
 ******************************************************************************************
 *
 * This file is for protons at 2.14 GeV/c
 *
 *
 * Execute with:  >madxp < psb_extraction.madx
 *
 ******************************************************************************************/



 title, 'BOOSTER lattice';

 option, echo;
 option, RBARC=FALSE;



/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/Psb/2013/psb.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/Psb/2013/psb.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/Psb/2013/psb.dbx';
 call, file = '/afs/cern.ch/eng/ps/cps/Psb/2013/strength/psb_extraction.str';


/******************************************************************************************
 * Select Ring - r1
 ******************************************************************************************/

psb: sequence, refer=ENTRY,  l = 157.08000;
  P01BOT                   , at =   0.00000;
  P02BOT                   , at =   9.81750;
  P03BOT                   , at =  19.63500;
  P04BOT                   , at =  29.45250;
  P05BOT                   , at =  39.27000;
  P06BOT                   , at =  49.08750;
  P07BOT                   , at =  58.90500;
  P08BOT                   , at =  68.72250;
  P09BOT                   , at =  78.54000;
  P10BOT                   , at =  88.35750;
  P11BOT                   , at =  98.17500;
  P12BOT                   , at = 107.99250;
  P13BOT                   , at = 117.81000;
  P14BOT                   , at = 127.62750;
  P15BOT                   , at = 137.44500;
  P16BOT                   , at = 147.26250;
endsequence;

!save, sequence=psb, file=psb.save;



/******************************************************************************************
 * beam, use
 ******************************************************************************************/

beam, particle=PROTON, pc=2.14;  ! pc=momentum
use, sequence=psb;

set,  format="20.10f";



/******************************************************************************************
 * Match for new working point
 ******************************************************************************************/

MATCH,sequence=psb;
 vary, NAME=kKF, step = 0.0001;
 vary, NAME=kKD, step = 0.0001;
 constraint, range=#E, MUX=4.172, MUY=4.230;
 lmdif, calls = 10000, tolerance = 1.0E-21;
ENDMATCH;



! Print results on file: match_extraction.prt
!assign, echo="../output/match_extraction.prt";
print, text="match_extraction";
value,  kKF;
value,  kKD;
assign, echo=terminal;



/******************************************************************************************
 * TWISS
 ******************************************************************************************/

PSHIFT=0;

/************************************************************
 * MAD uses pt as the 5th variable NOT delta-p.
 * The result is that all derivatives are with repect to pt.
 * This is the reason for the non-standard dispersion values
 * for non-relativistic machines, like the PSB
 ************************************************************/
beta=sqrt(1-1/beam->gamma^2);
disp:=beta*table(twiss,dx); ! Delta_x=Disp*Delta_p/p;


select, flag=twiss, clear;
select, flag=twiss, column=name, s, dx, disp;

twiss , centre
      , DELTAP = PSHIFT
      , table=TWISS
      , file='output_extraction.outxptc';
!      , file='../output/output_extraction.outxptc';

value,table(TWISS,PSB$START,betx);



/******************************************************************************************
 * PLOT
 ******************************************************************************************/

/* Use >gv psb2010.ps to plot */

PLOT, table=TWISS, VAXIS=BETX,BETY,HAXIS=S,
      SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, FILE='psb_extraction';
!      SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, FILE='../output/psb_extraction';
PLOT, table=TWISS, VAXIS=DISP,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;
PLOT, table=TWISS, VAXIS=ALFX,ALFY,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;





/******************************************************************************************
 * PTC
 ******************************************************************************************/

!--------------------------------------------------------------------------
! PTC_TWISS
!
! This PTC example illustrates the difference of the calculation
! of dispersion, either according to p or pt (momentum).
!
! The dispersions differs with Beta (relativistic): D_p = D_pt*Beta
!
! D_pt corresponds to time=true  i.e. according to pt (same result as TWISS)
! D_p  corresponds to time=false i.e. according to p
!
!--------------------------------------------------------------------------

select,flag=ptc_twiss,clear;
select,flag=ptc_twiss,column=name,s,disp1;


!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
ptc_create_universe;
ptc_create_layout,model=2,method=6,time=true,nst=5,exact; ! time = true

ptc_twiss,icase=5,no=1,file='twiss_ptc_true.outxptc';
!ptc_twiss,icase=5,no=1,file='../output/twiss_ptc_true.outxptc';
ptc_end;


!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
ptc_create_universe;
ptc_create_layout,model=2,method=6,time=false,nst=5,exact; ! time = false

ptc_twiss,icase=5,no=1,file='twiss_ptc_false.outxptc';
!ptc_twiss,icase=5,no=1,file='../output/twiss_ptc_false.outxptc';
ptc_end;





STOP;



