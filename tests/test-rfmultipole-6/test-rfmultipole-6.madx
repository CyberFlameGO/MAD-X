TITLE, 'RF-Dipole example';

!!!! data

Lq = 0.4;
Lb = 1.0;
Ld = (1.6 - Lb)/2.0;
Kf =  1.0; !  1.02780070155859;
Kd = -1.0; ! -1.02780070155859;

lcell = 2.0*Lq + 4.0*Ld + 2.0*Lb;
ncell = 12;
circum = ncell*lcell;

qf1:  quadrupole, l=Lq,     k1:=Kf;
qf1h: quadrupole, l=Lq/2.0, k1:=Kf;
qd1:  quadrupole, l=Lq,	    k1:=Kd;
mb:   sbend, 	  l=Lb, ANGLE=2.0*pi/ncell/2.0;
rfm1: rfmultipole, l=0, freq=12, knl:={1e-3},  pnl:={0.25};

!!!!! sequence
my_seq: sequence, refer=entry, l=ncell*lcell;
rfm1: rfm1, at=0.0;

n=1;
qf1h:	qf1h, at=(n-1)*lcell;
while (n<ncell) {
      mb:	mb,   at=(n-1)*lcell + Lq/2.0 + Ld;
      qd1:	qd1,  at=(n-1)*lcell + lcell/2.0 - Lq/2.0;
      mb:	mb,   at=(n-1)*lcell + lcell/2.0 + Lq/2.0 + Ld;
      qf1:	qf1, at=(n-1)*lcell + lcell - Lq/2.0;
      n=n+1;
}
mb:	mb,   at=(n-1)*lcell + Lq/2.0 + Ld;
qd1:	qd1,  at=(n-1)*lcell + lcell/2.0 - Lq/2.0;
mb:	mb,   at=(n-1)*lcell + lcell/2.0 + Lq/2.0 + Ld;
qf1h:	qf1h, at=(n-1)*lcell + lcell - Lq/2.0;
endsequence;

!!!!! beam
beam, particle = proton, sequence = my_seq, energy = 1,
      NPART=1.05E11, sige=4.5e-4 ;

!!!!! twiss
use, sequence = my_seq;
select, flag = twiss, column=name,s,betx,alfx,mux, bety, alfy,muy,dx,dy;
twiss, save, centre; ! file=twiss2c.out;
!	plot, haxis=s, vaxis=betx,bety,dx,colour=100;

!!!!! track
use, sequence=my_seq;
tr$macro(turn): macro = {
}

track, onepass, update, dump;
start, x=0, px=0, y=0, py=0, t=-0.01, pt=0;
start, x=0, px=0, y=0, py=0, t= 0.00, pt=0;
start, x=0, px=0, y=0, py=0, t= 0.01, pt=0;
run, turns=10;
endtrack;

!!!!! Convert to SixTrack

!use, period=my_seq;
!sixtrack, radius=0.017;

stop;
