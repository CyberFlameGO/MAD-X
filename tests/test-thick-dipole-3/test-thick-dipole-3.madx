option, -debug, -echo;

title, "thin_foc with edge effects";

set,format="22.16g"; ! format with many digits

lmb := 10.; !--- arc length of dipole
amb := 0.010; ! normally use this

factor := amb / (2*sin(amb/2) ); 
lstraight := lmb/factor;
show, lstraight;

! typical fint values range from 0.3 to 1,
! only have an effect if also the gap h is given,
! for the gap take something of the order 10 cm or 0.1

mb_fint =0.5;
mb_fintx=0.6;

mb_hgap=0.12;
mb_tilt=0.013;

kmb = 0; ee1 = 0.001; ee2 = 0.0001; ! 10 m long dipole, bending 10 mrad, so that rho = 1000 meter

mb1: sbend, l:=lmb, angle:=amb, k1:=kmb, e1:=ee1, e2:=ee2, fint=0.5, fintx=0.6, hgap =mb_hgap, tilt =mb_tilt;

lmb2 := lstraight; 

show,lmb2;
	
mb2: rbend, l:=lmb2, angle:=amb, fint=0.7, k1:=kmb, e2:=ee2, hgap =mb_hgap; ! as mb1 but rbend

myseq: sequence, l=3*lmb;
  mb1, at:= lmb/2;
  mb1, at:=1.5*lmb;
  mb2, at:=2.5*lmb;
endsequence;

! default is positron, 1 GeV,  use 1000 TeV to get beta close to 1  and dispersion terms to 1.e-13
beam,energy=1.e6; 
use, sequence=myseq;

select, flag=twiss, clear;
select, flag=twiss, column= name, s, l, x, px, hkick, betx, alfx, mux, dx, dpx, bety, alfy, dy, dpy, muy, angle, k0l, k1l, k2l, tilt, x, y, px, py, RE11,RE12,RE13,RE14,RE15,RE16,RE21,RE22,RE23,RE24,RE25,RE26,RE31,RE32,RE33,RE34,RE35,RE36,RE41,RE42,RE43,RE44,RE45,RE46,RE51,RE52,RE53,RE54,RE55,RE56,RE61,RE62,RE63,RE64,RE65,RE66;
twiss,rmatrix,betx=1,bety=1,file="twiss_thick.tfs"; ! with rmatrix to get values like   RE11

save, sequence=myseq, file="thick.seq";
use,  sequence=myseq;

select, flag=makethin, class=sbend, thick=true, slice=1; ! thick slice, with dipedge conversion    with dipedge  perfect  (to e-16)
select, flag=makethin, class=rbend, thick=true, slice=1; ! thick slice, with dipedge conversion    with dipedge  perfect  (to e-16)

makethin, sequence=myseq,style=teapot,makedipedge=true;

save, sequence=myseq, file="thin.seq";
use,  sequence=myseq;

twiss,rmatrix, betx=1,bety=1,file="twiss_thin.tfs";

track, onepass, dump;
start, x=1e-6, px=0, y=1e-6, py=0, t=-0.25;
start, x=1e-6, px=0, y=1e-6, py=0, t=-1.0;
start, x=1e-6, px=0, y=1e-6, py=0, t=-1.75;
run, turns=50, ffile=1;
endtrack;
stop;
