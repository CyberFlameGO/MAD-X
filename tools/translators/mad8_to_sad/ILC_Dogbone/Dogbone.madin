! TESLA Dogbone Damping Ring
! Optics Version 6.4.1 by Winni Decking (DESY) 06/03/02
! File Reformatted by Andy Wolski (LBNL)       25/08/04

TITLE, "TESLA DOGBONE DAMPING RING"

E0      := 5.0

!----------------------------------------------------
! BPMs, KICKERs, and MARKERs
!----------------------------------------------------

BPM:   MONITOR
COR:   KICKER   ! This is an orbit corrector, not an injection/ejection kicker!
MRK:   MARKER
MRKL:  MARKER
MRKA:  MARKER
MRKAD: MARKER
MRKW:  MARKER

!----------------------------------------------------
! Straights
!----------------------------------------------------

DLL:   DRIFT,      L=8.769
DLL3:  DRIFT,      L=26.307
DLL6:  DRIFT,      L=52.614
DL3:   DRIFT,      L=26.207

!BV:    RBEND,      L=0.1, ANGLE=0.76E-4, TILT=1.570796326795
BV:    RBEND,      L=0.1, ANGLE=0, TILT=1.570796326795

QLF:   QUADRUPOLE, L=0.2, K1=-0.072550351291
QLD:   QUADRUPOLE, L=0.2, K1= 0.072550351148

DL:    LINE=(BPM,COR,DLL6)
DL1:   LINE=(BPM,COR,DLL3)
DL2:   LINE=(DLL3)
DL2BV: LINE=(DL3,BV)

L_CELL:    LINE=(MRKL, DL2,  QLD,DL,QLF,DL1)
L_CELL_BV: LINE=(MRKL, DL2BV,QLD,DL,QLF,DL1)

STRAIGHT:  LINE=(L_CELL, 16*(L_CELL_BV,3*L_CELL), L_CELL)

!----------------------------------------------------
! Arc Cells
!----------------------------------------------------

DA11:  DRIFT,      L=0.5
DA12:  DRIFT,      L=0.2
DA21:  DRIFT,      L=1.3
DA31:  DRIFT,      L=0.2
DAD1:  DRIFT,      L=7.98

BENDLEN := 9.0
BENDANG := 0.10471975512

BP:    SBEND,      L=BENDLEN,   ANGLE= BENDANG
BP2:   SBEND,      L=BENDLEN/2, ANGLE= BENDANG/2 
BM:    SBEND,      L=BENDLEN,   ANGLE=-BENDANG
BM2:   SBEND,      L=BENDLEN/2, ANGLE=-BENDANG/2 

QAD:   QUADRUPOLE, L=0.2, K1=-1.044603994734
QAF:   QUADRUPOLE, L=0.2, K1= 1.268547204891
QAD1:  QUADRUPOLE, L=0.1, K1= 0.420077211378
QAD1L: QUADRUPOLE, L=0.2, K1= 0.420077211378
QAD2:  QUADRUPOLE, L=0.2, K1=-0.436731950892

SEXTON  := 1
SXT1PK2 := -5.122*SEXTON
SXT1MK2 :=  5.122*SEXTON
SXT2PK2 :=  6.380*SEXTON
SXT2MK2 := -6.380*SEXTON

S1P:   SEXTUPOLE,  L=0.3, K2= SXT1PK2
S1M:   SEXTUPOLE,  L=0.3, K2= SXT1MK2
S2P:   SEXTUPOLE,  L=0.2, K2= SXT2PK2
S2PL:  SEXTUPOLE,  L=0.4, K2= SXT2PK2
S2M:   SEXTUPOLE,  L=0.2, K2= SXT2MK2
S2ML:  SEXTUPOLE,  L=0.4, K2= SXT2MK2

SKEW:  MULTIPOLE,  K1L=0, T1

DAP1:  LINE=(DA11,S1P,SKEW,DA12)
DAM1:  LINE=(DA11,S1M,SKEW,DA12)
DAD:   LINE=(COR,BPM,DAD1)
DA2:   LINE=(COR,BPM,DA21)
DA3:   LINE=(DA31,COR,BPM)

ARC_PCELL_L:   LINE=(MRKA,S2PL,SKEW,DA3,QAF,DAP1,QAD,DA2, &
                     BP2,BP2,-DA2,QAD,-DAP1,QAF,-DA3)

ARC_MCELL_L:   LINE=(MRKA,S2ML,SKEW,DA3,QAF,DAM1,QAD,DA2, &
                     BM2,BM2,-DA2,QAD,-DAM1,QAF,-DA3)

ARC_PCELL_ANF: LINE=(MRKA,S2P,SKEW,DA3,QAF,DAP1,QAD,DA2,  &
                     BP2,BP2,-DA2,QAD,-DAP1,QAF,-DA3)

ARC_MCELL_ANF: LINE=(MRKA,S2M,SKEW,DA3,QAF,DAM1,QAD,DA2,  &
                     BM2,BM2,-DA2,QAD,-DAM1,QAF,-DA3)

ARC_DRIFT_L:   LINE=(MRKAD,QAD1L,DAD,QAD2,DAD)

ARC_DRIFT_ANF: LINE=(MRKAD,QAD1, DAD,QAD2,DAD)

!----------------------------------------------------
! Arc Matching
!----------------------------------------------------

DM11:  DRIFT,      L=0.5
DM12:  DRIFT,      L=0.2
DM21:  DRIFT,      L=4.3
DM31:  DRIFT,      L=2.74444156633
DM41:  DRIFT,      L=1.1
DM51:  DRIFT,      L=0.0

QAM1:  QUADRUPOLE, L=0.2, K1=1.270733877368
QAM2:  QUADRUPOLE, L=0.2, K1=-1.047050721492
QAM3:  QUADRUPOLE, L=0.2, K1=-0.986607598445
QAM4:  QUADRUPOLE, L=0.2, K1=1.210408991472
QAM5:  QUADRUPOLE, L=0.3, K1=-0.219

SHO1P: SEXTUPOLE,  L=0.3, K2=0.0
SHO1M: SEXTUPOLE,  L=0.3, K2=0.0
SHO2P: SEXTUPOLE,  L=0.2, K2=0.0
SHO2M: SEXTUPOLE,  L=0.2, K2=0.0

DMP1:           LINE=(DM11,SHO1P,DM12)
DMM1:           LINE=(DM11,SHO1M,DM12)
DM2:            LINE=(COR,BPM,DM21)
DM3:            LINE=(DM31)
DM4:            LINE=(COR,BPM,DM41)
DM5:            LINE=(COR,BPM,DM51,COR,BPM)

ARC_NOD2DMATCH: LINE=(DM3,QAM3,DM4,QAM4,DM5)

ARC_PNOD:       LINE=(SHO2P,DA3,QAM1,DMP1,QAM2,DM2,BP2)
ARC_P2NOD:      LINE=(ARC_PNOD,ARC_NOD2DMATCH)

ARC_MNOD:       LINE=(SHO2M,DA3,QAM1,DMM1,QAM2,DM2,BM2)
ARC_M2NOD:      LINE=(ARC_MNOD,ARC_NOD2DMATCH)

!----------------------------------------------------
! Arc
!----------------------------------------------------

ARC_L:          LINE=(-ARC_P2NOD,ARC_PCELL_ANF,39*ARC_PCELL_L,  &
             S2P,SKEW, ARC_P2NOD,ARC_DRIFT_ANF, 6*ARC_DRIFT_L,  &
                 QAD1,-ARC_M2NOD,ARC_MCELL_ANF, 9*ARC_MCELL_L,  &
             S2M,SKEW, ARC_M2NOD)

!----------------------------------------------------
! Wiggler
!----------------------------------------------------

WD:    DRIFT, L=0.09
WD2:   DRIFT, L=0.0225
DW:    DRIFT, L=0.4675
WIG0:  DRIFT, L=3.865

WIGLEN := 0.110
WIGANG := 1.055300896E-2

WP1:   SBEND, L=WIGLEN/2, ANGLE= WIGANG/2, E1= WIGANG/2, E2= 0.0
WP3:   SBEND, L=WIGLEN/2, ANGLE= WIGANG/2, E1= 0.0,      E2= WIGANG/2
WP2:   SBEND, L=WIGLEN,   ANGLE= WIGANG,   E1= WIGANG/2, E2= WIGANG/2

WM1:   SBEND, L=WIGLEN/2, ANGLE=-WIGANG/2, E1=-WIGANG/2, E2= 0.0
WM3:   SBEND, L=WIGLEN/2, ANGLE=-WIGANG/2, E1= 0.0,      E2=-WIGANG/2
WM2:   SBEND, L=WIGLEN,   ANGLE=-WIGANG,   E1=-WIGANG/2, E2=-WIGANG/2

WIGPERD:  LINE=(WP2,WD,WM2,WD)
WIGGLER:  LINE=(WM3,WD2,9*WIGPERD,WP2,WD2,WM1)

QFW:   QUADRUPOLE, L=0.1, K1=-0.686739796299
QDW:   QUADRUPOLE, L=0.1, K1= 0.889693683188
QFW0:  QUADRUPOLE, L=0.1, K1=-0.861760693345
QDW0:  QUADRUPOLE, L=0.1, K1= 1.03828501124
QFWL:  QUADRUPOLE, L=0.2, K1=-0.686739796299
QDWL:  QUADRUPOLE, L=0.2, K1= 0.889693683188
QFWL0: QUADRUPOLE, L=0.2, K1=-0.861760693345
QDWL0: QUADRUPOLE, L=0.2, K1= 1.03828501124

WIG_CELL_L:    LINE=(MRKW, QDWL, COR,BPM,DW,WIGGLER,DW, &
                           QFWL, COR,BPM,DW,WIGGLER,DW)

WIG_CELL_ANF:  LINE=(MRKW, QDW,  COR,BPM,DW,WIGGLER,DW, &
                           QFWL, COR,BPM,DW,WIGGLER,DW)

WIG0_CELL_L:   LINE=(MRKW, QDWL0,COR,BPM,DW,WIG0,   DW, &
                           QFWL0,COR,BPM,DW,WIG0,   DW)

WIG0_CELL_ANF: LINE=(MRKW, QDW0, COR,BPM,DW,WIG0,   DW, &
                           QFWL0,COR,BPM,DW,WIG0,   DW)

WIG_SECT:      LINE=(WIG0_CELL_ANF,5*WIG0_CELL_L,QDW0,&
                      WIG_CELL_ANF,26*WIG_CELL_L,QDW,COR,BPM)

!----------------------------------------------------
! Wiggler Matching
!----------------------------------------------------

DWR11:  DRIFT, L=2.0
DWR22:  DRIFT, L=7.0
DWR33:  DRIFT, L=1.0

DWA11:  DRIFT, L=1.5
DWA21:  DRIFT, L=6.0
DWA31:  DRIFT, L=1.5

DWAL11: DRIFT, L=1.5
DWAL21: DRIFT, L=7.85276
DWAL31: DRIFT, L=1.5

DWR1:   LINE=(BPM,COR,DWR11)
DWR2:   LINE=(BPM,COR,DWR22)
DWR3:   LINE=(BPM,COR,DWR33)

DWA1:   LINE=(BPM,COR,DWA11)
DWA2:   LINE=(BPM,COR,DWA21)
DWA3:   LINE=(BPM,COR,DWA31)

DWAL1:  LINE=(BPM,COR,DWAL11)
DWAL2:  LINE=(BPM,COR,DWAL21)
DWAL3:  LINE=(BPM,COR,DWAL31)

QWR1:   QUADRUPOLE, L=0.3, K1= 0.651437909526
QWR2:   QUADRUPOLE, L=0.4, K1=-0.346899662191
QWR3:   QUADRUPOLE, L=0.4, K1=-0.155106384663
QWR4:   QUADRUPOLE, L=0.4, K1= 0.249006648826
QWR5:   QUADRUPOLE, L=0.4, K1=-0.466173850305
QWR6:   QUADRUPOLE, L=0.4, K1= 0.0
QWR7:   QUADRUPOLE, L=0.4, K1= 0.328964535697
QWR8:   QUADRUPOLE, L=0.4, K1=-0.125568508032
QWR9:   QUADRUPOLE, L=0.4, K1=-0.170937316189

QWR1R:  QUADRUPOLE, L=0.3, K1= 0.454592650704
QWR2R:  QUADRUPOLE, L=0.4, K1=-0.307942534841
QWR3R:  QUADRUPOLE, L=0.4, K1=-0.175576497929
QWR4R:  QUADRUPOLE, L=0.4, K1= 0.250759172468
QWR5R:  QUADRUPOLE, L=0.4, K1=-0.616115760016
QWR6R:  QUADRUPOLE, L=0.4, K1= 0.0
QWR7R:  QUADRUPOLE, L=0.4, K1= 0.377631315766
QWR8R:  QUADRUPOLE, L=0.4, K1=-0.158151201299
QWR9R:  QUADRUPOLE, L=0.4, K1=-0.197095008516

QWA1:   QUADRUPOLE, L=0.3, K1= 0.843663525615
QWA2:   QUADRUPOLE, L=0.4, K1=-0.357358808696
QWA3:   QUADRUPOLE, L=0.4, K1=-0.543495671327
QWA4:   QUADRUPOLE, L=0.2, K1= 1.227029261356

QWAL1:  QUADRUPOLE, L=0.3, K1= 0.591770641554
QWAL2:  QUADRUPOLE, L=0.4, K1=-0.464780444714
QWAL5:  QUADRUPOLE, L=0.4, K1=-0.316609082616
QWAL6:  QUADRUPOLE, L=0.4, K1= 0.433993066794
QWAL3:  QUADRUPOLE, L=0.4, K1=-0.473836669966
QWAL4:  QUADRUPOLE, L=0.2, K1= 0.721217818408

W2R_MATCH:     LINE=(QWR1,DWR1,QWR2,DWR1,QWR3,DWR2,QWR4,DWR3,QWR5, &
                     DWR3,QWR4,DWR2,QWR7,DWR1,QWR8,DWR1,QWR9)

W2R_MATCH_ROL: LINE=(QWR1R,DWR1,QWR2R,DWR1,QWR3R,DWR2,QWR4R,DWR3,  &
                     QWR5R,DWR3,QWR4R,DWR2,QWR7R,DWR1,QWR8R,DWR1,QWR9R)

W2A_MATCH:     LINE=(QWA1,DWA1,QWA2,DWA2,QWA3,DWA3,QWA4)

W2AL_MATCH:    LINE=(QWAL1,DWAL1,QWAL2,DWAL2,QWAL5,DWAL1,QWAL6,DWAL2, &
                     QWAL3,DWAL3,QWAL4)

!----------------------------------------------------
! Coupling Bumps
!----------------------------------------------------

DR11:  DRIFT,      L= 2.908883481303
DR22:  DRIFT,      L=12.439771338177

SQR1:  QUADRUPOLE, L=0.4, K1=-0.156494237892, TILT=0.785398163397
SQR2:  QUADRUPOLE, L=0.4, K1= 0.183999813122, TILT=0.785398163397
SQR1M: QUADRUPOLE, L=0.4, K1= 0.156494237892, TILT=0.785398163397
SQR2M: QUADRUPOLE, L=0.4, K1=-0.183999813122, TILT=0.785398163397

QR1:   QUADRUPOLE, L=0.4, K1=-0.156494237892
QR2:   QUADRUPOLE, L=0.4, K1=0.183999813122

DR1:           LINE=(BPM,COR,DR11)
DR2:           LINE=(BPM,COR,DR22)

! Coupling Bumps OFF

ROT:           LINE=(DR1, QR1, DR2, QR2, DR2, QR1, DR1)

! Coupling Bumps ON

ROTROL1:       LINE=(DR1,SQR1, DR2,SQR2, DR2,SQR1, DR1)
ROTROL2:       LINE=(DR1,SQR1M,DR2,SQR2M,DR2,SQR1M,DR1)

!----------------------------------------------------
! Coupling Bumps Matching
!----------------------------------------------------

DLR11: DRIFT,      L= 2.0
DLR22: DRIFT,      L=50.0

QLR1:  QUADRUPOLE, L=0.2, K1= 0.301485819671
QLR2:  QUADRUPOLE, L=0.2, K1=-0.198862399728
QLR3:  QUADRUPOLE, L=0.2, K1=-0.424596486095
QLR4:  QUADRUPOLE, L=0.2, K1= 0.39777577694
QLR5:  QUADRUPOLE, L=0.2, K1=-0.39777577694
QLR6:  QUADRUPOLE, L=0.2, K1= 0.424596486095
QLR7:  QUADRUPOLE, L=0.2, K1= 0.198862399728
QLR8:  QUADRUPOLE, L=0.2, K1=-0.301485819671

DLR1:          LINE=(BPM,COR,DLR11)
DLR2:          LINE=(BPM,COR,DLR22)

R2L_MATCH_IN:  LINE=(QLR1,DLR1,QLR2,DLR2,QLR3,DLR1,QLR4,DLR1)
R2L_MATCH_OUT: LINE=(DLR1,QLR5,DLR1,QLR6,DLR2,QLR7,DLR1,QLR8)

!----------------------------------------------------
! RF Cavity
!----------------------------------------------------

CAV: RFCAVITY, L=0.0001, VOLT=25.0, LAG=0.429, HARMON=28200

!----------------------------------------------------
! Beamlines
!----------------------------------------------------

RING: &     
LINE=(MRK, -WIG_SECT, CAV, MRK, W2R_MATCH,     &
      MRK,  ROT,           MRK, R2L_MATCH_IN,  &
      MRK,  STRAIGHT,      MRK, R2L_MATCH_OUT, &
      MRK,  ROT,           MRK,-W2R_MATCH,     &
      MRK,  WIG_SECT, CAV, MRK, W2A_MATCH,     &
      MRK,  ARC_L,         MRK,-W2AL_MATCH,    W2R_MATCH,  &
      MRK,  ROT,           MRK, R2L_MATCH_IN,  &
      MRK,  STRAIGHT,      MRK, R2L_MATCH_OUT, &
      MRK,  ROT,           MRK,-W2R_MATCH,     W2AL_MATCH, &
      MRK, -ARC_L,         MRK,-W2A_MATCH)

RING_ROT: &
LINE=(MRK, -WIG_SECT, CAV, MRK, W2R_MATCH_ROL, &
      MRK,  ROTROL1,       MRK, R2L_MATCH_IN,  &
      MRK,  STRAIGHT,      MRK, R2L_MATCH_OUT, &
      MRK, -ROTROL2,       MRK,-W2R_MATCH_ROL, &
      MRK,  WIG_SECT, CAV, MRK, W2A_MATCH,     &
      MRK,  ARC_L,         MRK,-W2AL_MATCH,    W2R_MATCH_ROL, &
      MRK,  ROTROL1,       MRK, R2L_MATCH_IN,  &
      MRK,  STRAIGHT,      MRK, R2L_MATCH_OUT, &
      MRK, -ROTROL2,       MRK,-W2R_MATCH_ROL, W2AL_MATCH,    &
      MRK, -ARC_L,         MRK,-W2A_MATCH)

STRT: &
LINE=(-WIG_SECT, CAV, W2R_MATCH,     &
       ROT,           R2L_MATCH_IN,  &
       STRAIGHT,      R2L_MATCH_OUT, &
       ROT,          -W2R_MATCH,     &
       WIG_SECT, CAV, W2A_MATCH,     &
      -W2AL_MATCH,    W2R_MATCH,  &
       ROT,           R2L_MATCH_IN,  &
       STRAIGHT,      R2L_MATCH_OUT, &
       ROT,          -W2R_MATCH,     W2AL_MATCH, &
      -W2A_MATCH)

RETURN !



