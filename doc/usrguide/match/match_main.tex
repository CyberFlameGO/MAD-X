%%\title{MATCH / ENDMATCH}
%  Changed by: Chris ISELIN, 27-Jan-1997 
%  Changed by: Oliver Bruning, 20-Jun-2002 
%  Changed by: Hans Grote, 30-Sep-2002 
%  Changed by: Riccardo de Maria, 9-Jan-2008 

\section{ Enter and Leave \href{match.html}{Matching Mode}}

Before matching at least one
\href{../Introduction/sequence.html}{SEQUENCE} must be selected by means
of a \href{../control/general.html#use}{USE} command. The matching
module can act on more than one sequence simultaneously by specifying
more than one sequence when
\href{../match/match_main.html#match}{INITIATING} the matching mode: 
 
\subsection{Initiating the Matching Module}

The 'match' command can be either used for matching a periodic cell or
for matching an insertion to another part of the machine. Both matching
modes are initiated by the MATCH command. 

\begin{itemize}
	\item Cell matching:
In the first mode the matching routine is initiated only with one
attribute specifying the sequence(s) the matching module will work
on. In this matching mode the periodicity of the optics functions is
enforced at the beginning and end of the selected range. 

MATCH, SEQUENCE='name1', 'name2',..,nema-n';
 
	\item Insertion matching:
In the second mode, called insertion matching, the matching routine is
initiated with two attributes: one specifying the sequence(s) the
matching module will work on and one specifying the initial conditions
of the optic functions for each sequence. In this case the initial
values are assumed as exact. 
 
\begin{itemize}
	\item 
Specification of Initial Values: The initial values of the optical
functions  for the insertion matching can either be specified in form of
a \href{../control/general.html#savebeta}{SAVEBETA} command or by
explicitly stating the individual optic function values after the
'MATCH' command. The two options can be entered as 
 
\begin{verbatim}
MATCH, sequence= 'name1', 'name2',.., 'name-n',
       BETA0= 'beta01', 'beta02',..., 'beta0n';
\end{verbatim}
or
\begin{verbatim}
MATCH, SEQUENCE='sequence-name', BETX=real,ALFX=real,MUX=real,
                                 BETY=real,ALFY=real,MUY=real,
                                 X=real,PX=real,Y=real,PY=real,
                                 DX=real,DY=real,DPX=real,DPY=real,
                                 DELTAP=real;
\end{verbatim}

{\bf Examples:}
 
Example 1:
\begin{verbatim}
CELL: SEQUENCE=(...) ;
INSERT: SEQUENCE=(...) ;
USE,PERIOD=cell;
SAVEBETA,LABEL=bini,place=#e;
TWISS,SEQUENCE=cell;
USE,PERIOD=insert;
MATCH,SEQUENCE=insert,BETA0=bini;
CONSTRAINT,SEQUENCE=insert,RANGE=#e,MUX=9.345,MUY=9.876;
\end{verbatim}
This matches the sequence 'INSERT' with initial conditions to a new
phase advance. The initial conditions are given by the periodic solution
for the sequence CELL1. 

Example 2:	
\begin{verbatim}
USE,PERIOD=INSERT;
MATCH,SEQUENCE=insert;
CONSTRAINT,SEQUENCE=insert,RANGE=#e,MUX=9.345,MUY=9.876;
\end{verbatim}
This matches the beam line 'INSERT' with periodic boundary conditions to
a new phase advance. 


 
The initial conditions can also be transmitted by a combination of a
\href{../control/general.html#savebeta}{SAVEBETA} command and explicit
optic function specifications: 
\begin{verbatim}
USE,CELL1;
SAVEBETA,LABEL=bini,PLACE=#E;
TWISS,SEQUENCE=CELL1;
USE,PERIOD=LINE1;
MATCH,SEQUENCE=LINE1,BETA0=bini,MUX=1.234,MUY=4.567;
\end{verbatim}

This example transmits all values of the SAVEBETA array 'bini' as
initial values to the MATCH command and overrides the initial phase
values by the given values.

\end{itemize}

An additional \href{match_con.html#constraint}{CONSTRAINT} may be
imposed in other places, i.e. intermediate or end values of the optics
functions at the transition point.  
 
	\item More than one active sequence:

The matching module can act on more than one sequence simultaneously by
specifying more than one sequence after the MATCH command: 
\begin{verbatim}
MATCH, SEQUENCE=LINE1, CELL1, BETA0=bini1, bini2;
\end{verbatim}
This example initiates the matching mode for the 'LINE1' and the 'CELL1'
sequence. The \href{../twiss/twiss.html}{Twiss module} function of the
two sequences are calculated with fixed initial conditions. The SAVEBETA
array 'bini1' is used for calculating the optics functions of sequence
'LINE1' and the SAVEBETA array 'bini2' for calculating the optics
functions of sequence 'CELL1'. Without the initial conditions the
matching module will work in the \href{match_main.html#cell}{CELL}
mode. 
 
	\item  Special flag:

The "slow" attribute enforces the old and slow matching procedure which
allows to use the special columns \texttt{mvar1, ..., mvar4}, if they
are added to the twiss table. Recently a number of parameter, like
"RE56", have been added to list of matchable parameters. Nevertheless,
some parameters might only be available when using the "slow" attribute. 
 
\end{itemize}
 
\subsection{Further attributes of the TWISS statements are:}

\begin{itemize}
	\item 
 RMATRIX: If this flag is used the one-turn map at the location of every
 element is calculated and prepared for storage in the TWISS table.
\\Target values for the matrix elements at certain positions in the sequence
 can be specified with the help of the \href{match_con.html#constraint}{CONSTRAINT}
 command and the
 keywords: \textbf{RE, RE11...RE16...RE61...RE66}, where \textbf{REij} refers
 to the "ij" matrix component.
  
\textgreater Examples:
 
\begin{verbatim}
     Example 1:
     MATCH,RMATRIX,SEQUENCE='name',BETA0='beta-block-name';
     CONSTRAINT,SEQUENCE=insert,RANGE=#e,RE11=-2.808058321,re22=2.748111197;
     VARY,NAME=kqf,STEP=1.0e-6;
     VARY,NAME=kqd,STEP=1.0e-6;
\end{verbatim}

This matches the sequence 'name' with initial conditions to new values
for the matrix elements 'RE11' and 'RE22' by varying the strength of the
main quadrupole circuits.
\end{itemize}

\begin{itemize}
	\item 
CHROM: If this flag is used the chromatic functions at the location of
every element are calculated and prepared for storage in the TWISS
table. 

Target values for the chromatic functions at certain positions in the
sequence can be specified with the help of the
\href{match_con.html#constraint}{CONSTRAINT} command and the keywords
\href{../Introduction/tables.html#normal}{WX, PHIX, WY, PHIY,...}. 
\end{itemize}

\subsection{Leave Matching Mode}
 
The ENDMATCH command terminates the matching section and deletes all tables related to the matching run.
\begin{verbatim}
ENDMATCH;
\end{verbatim}



%\href{http://bruening.home.cern.ch/bruening/}{Oliver Br\"uning}, October, 2003;
%\href{http://rdemaria.home.cern.ch/rdemaria/}{Riccardo de Maria}, January, 2008.
