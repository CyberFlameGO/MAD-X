%%\title{Expression Matching with USE\_MACRO}
%  Changed by: Chris ISELIN, 27-Jan-1997 
%  Changed by: Oliver Bruning, 20-Jun-2002 
%  Changed by: Hans Grote, 30-Sep-2002 

\section{ Introduction }
 
It is possible to match user defined expressions with the USE\_MACRO keyword.
The general input structure for a match command is the following:

\begin{verbatim}
MATCH,USE_MACRO;
... VARY statements ...
USE_MACRO, NAME=macro1;
     or
macro1: MACRO={ ... madx statements};
CONSTRAINT, expr=  "lhs1 < | = | > rhs1";
CONSTRAINT, expr=  "lhs2 < | = | > rhs2";
...  CONSTRAINT statements ...
MACRO 2 definition
... CONSTRAINT statements ...
MACRO n definition
... CONSTRAINT statements ...
... METHODS statements ...
ENDMATCH;
\end{verbatim}
 
The algorithm for evaluating the penalty function is the following:
 
\begin{itemize}
	\item  execute the first macro,
	\item  evaluate and compute the difference between the lhs and the rhs the first set of expressions,
	\item in case of other macros, evaluates in order the macro and the expressions
	\item  the set of differences are  minimized by the selected method using the variables defined in the VARY statements.
\end{itemize}

\subsection{Initiating the Matching Module with USE\_MACRO}
 
 With:
 
\begin{verbatim}
   MATCH,USE_MACRO;
\end{verbatim}

the 'match' command can be used for matching any expression which can be
defined through expression. It requires a slightly different syntax.

\subsection{VARY statements}
In the USE\_MACRO mode the vary statement follows the same rules of the other modes explained in the section \href{match_vary.html}{Define Variable Parameter}

\subsection{Macro definitions}
The macro to be used in the matching routine can be defined in two ways:
 
\begin{itemize}
	\item using USE\_MACRO statement:

\begin{verbatim}
 USE_MACRO, NAME=macro1;
\end{verbatim}

defining a new macro on the fly using the usual syntax for \href{../control/special.html#macro}{ macros}. 
\end{itemize}
 
After a macro definition is necessary to define a set of constraints exclusively with the following syntax:
 
\begin{verbatim}
 CONSTRAINT, expr=  "lhs = rhs"; 
\end{verbatim}

or 
\begin{verbatim}
 CONSTRAINT, expr=  "lhs < rhs"; 
\end{verbatim}

or
\begin{verbatim}
 CONSTRAINT, expr=  "lhs > rhs"; 
\end{verbatim}

where "lhs" and "rhs" are well defined MadX \href{../Introduction/expression.html}{expressions}. Other set of macro and constraints can be defined afterwards.

\subsection{Examples}
The following example the USE\_MACRO mode can emulate a matching script which uses the normal syntax.

Normal syntax:

\begin{verbatim}
MATCH,SEQUENCE=LHCB1,LHCB2;
    VARY, NAME=KSF.B1, STEP=0.00001;
    VARY, NAME=KSD.B1, STEP=0.00001;
    VARY, NAME=KSF.B2, STEP=0.00001;
    VARY, NAME=KSD.B2, STEP=0.00001;
    GLOBAL,SEQUENCE=LHCB1,DQ1=QPRIME;
    GLOBAL,SEQUENCE=LHCB1,DQ2=QPRIME;
    GLOBAL,SEQUENCE=LHCB2,DQ1=QPRIME;
    GLOBAL,SEQUENCE=LHCB2,DQ2=QPRIME;
    LMDIF, CALLS=10, TOLERANCE=1.0E-21;
ENDMATCH;
\end{verbatim}

USE\_MACRO syntax:

\begin{verbatim}
MATCH,USE_MACRO;
    VARY, NAME=KSF.B1, STEP=0.00001;
    VARY, NAME=KSD.B1, STEP=0.00001;
    VARY, NAME=KSF.B2, STEP=0.00001;
    VARY, NAME=KSD.B2, STEP=0.00001;
    M1: MACRO={ TWISS,SEQUENCE=LHCB1; };
    CONSTRAINT, EXPR= TABLE(SUMM,DQ1)=QPRIME;
    CONSTRAINT, EXPR= TABLE(SUMM,DQ2)=QPRIME;
    M2: MACRO={ TWISS,SEQUENCE=LHCB2; };
    CONSTRAINT, EXPR= TABLE(SUMM,DQ1)=QPRIME;
    CONSTRAINT, EXPR= TABLE(SUMM,DQ2)=QPRIME;
    LMDIF, CALLS=10, TOLERANCE=1.0E-21;
ENDMATCH;
\end{verbatim}

%\line(1,0){300}

%\href{http://bruening.home.cern.ch/bruening/}{Oliver Br\"uning},October, 2003;
%\href{http://rdemaria.home.cern.ch/rdemaria/}{Riccardo de Maria}, February, 2006.